//-----------------Sonic CD TagaTaga Script-------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value1		:	Object.YVelocity
#alias Object.Value2		:	Object.YOriginPos

#alias Object.PropertyValue	:	Object.Quality

// States
#alias 0	:	TAGATAGA_SET_LAUNCH_SPEED
#alias 1	:	TAGATAGA_EMERGE
#alias 2	:	TAGATAGA_MOVE_PINCH_1
#alias 3	:	TAGATAGA_TURN_1
#alias 4	:	TAGATAGA_TURN_2
#alias 5	:	TAGATAGA_TURN_3
#alias 6	:	TAGATAGA_MOVE_PINCH_2
#alias 7	:	TAGATAGA_SUBMERGE

// Stage SFX
#alias 3	:	SFX_S_WATERSPLASH

// Badnik Quality / Property Values
#alias 0	:	GOOD_QUALITY
#alias 1	:	BAD_QUALITY

// Priority
#alias 0	:	PRIORITY_BOUNDS
#alias 1	:	PRIORITY_ACTIVE


sub ObjectMain
	if Object.Quality == GOOD_QUALITY

		switch Object.State
		case TAGATAGA_SET_LAUNCH_SPEED
			Object.YVelocity = -0x40000

			Object.Timer = 1

			Object.State = TAGATAGA_EMERGE
			break

		case TAGATAGA_EMERGE
			Object.YVelocity += 0x1000
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 24
				Object.State = TAGATAGA_MOVE_PINCH_1
			end if
			break

		case TAGATAGA_MOVE_PINCH_1
			Object.YVelocity += 0x1000
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 48
				Object.State = TAGATAGA_TURN_1
			end if
			break

		case TAGATAGA_TURN_1
			Object.YVelocity += 0x1000
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 56
				Object.State = TAGATAGA_TURN_2
			end if
			break

		case TAGATAGA_TURN_2
			Object.YVelocity += 0x1000
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 64
				Object.State = TAGATAGA_TURN_3
			end if
			break

		case TAGATAGA_TURN_3
			Object.YVelocity += 0x1000
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 96
				Object.State = TAGATAGA_MOVE_PINCH_2
			end if
			break

		case TAGATAGA_MOVE_PINCH_2
			Object.YVelocity += 0x1000
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 128
				Object.State = TAGATAGA_SUBMERGE
				PlayStageSfx(SFX_S_WATERSPLASH, false)lse)
			end if
			break

		case TAGATAGA_SUBMERGE
			Object.Timer++
			if Object.Timer > 204
				Object.State = TAGATAGA_SET_LAUNCH_SPEED

				Object.Timer = 0

				Object.YPos = Object.YOriginPos
				PlayStageSfx(SFX_S_WATERSPLASH, false)
			end if
			break

		end switch

	else
		switch Object.State
		case TAGATAGA_SET_LAUNCH_SPEED
			Object.YVelocity = -0x30000

			Object.Timer = 1

			Object.State = TAGATAGA_EMERGE
			break

		case TAGATAGA_EMERGE
			Object.YVelocity += 0xEC4
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 40
				Object.State = TAGATAGA_MOVE_PINCH_1
			end if
			break

		case TAGATAGA_MOVE_PINCH_1
			Object.YVelocity += 0xEC4
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 50
				Object.State = TAGATAGA_TURN_1
			end if
			break

		case TAGATAGA_TURN_1
			Object.YVelocity += 0xEC4
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 60
				Object.State = TAGATAGA_TURN_2
			end if
			break

		case TAGATAGA_TURN_2
			Object.YVelocity += 0xEC4
			Object.YPos += Object.YVelocity

			Object.Timer++
			if Object.Timer > 104
				Object.State = TAGATAGA_TURN_3
				PlayStageSfx(SFX_S_WATERSPLASH, false)
			end if
			break

		case TAGATAGA_TURN_3
			Object.Timer++
			if Object.Timer > 188
				Object.State = TAGATAGA_SET_LAUNCH_SPEED

				Object.Timer = 0

				Object.YPos = Object.YOriginPos
				PlayStageSfx(SFX_S_WATERSPLASH, false)
			end if
			break

		end switch

	end if
	CallFunction(StageSetup_CheckGoodFuture)	// Check if it should be a flower
end sub


sub ObjectPlayerInteraction
	if Object.Quality == GOOD_QUALITY
		if Object.State < TAGATAGA_SUBMERGE
			PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
			if CheckResult == true
				CallFunction(Player_BadnikBreak)
			end if
		end if
	else
		if Object.State < TAGATAGA_TURN_3
			PlayerObjectCollision(C_TOUCH, -16, -16, 16, 16)
			if CheckResult == true
				CallFunction(Player_BadnikBreak)
			end if
		end if
	end if
end sub


sub ObjectDraw
	if Object.Quality == GOOD_QUALITY
		switch Object.State
		case TAGATAGA_SET_LAUNCH_SPEED
		case TAGATAGA_EMERGE
			DrawSprite(0)
			break

		case TAGATAGA_MOVE_PINCH_1
			TempValue0  = Object.Timer
			TempValue0 &= 15
			if TempValue0 < 8
				DrawSprite(0)
			else
				DrawSprite(1)
			end if
			break

		case TAGATAGA_TURN_1
			DrawSprite(2)
			break
			
		case TAGATAGA_TURN_2
			DrawSprite(3)
			break

		case TAGATAGA_TURN_3
			DrawSprite(4)
			break

		case TAGATAGA_MOVE_PINCH_2
			TempValue0  = Object.Timer
			TempValue0 &= 15
			if TempValue0 < 8
				DrawSprite(4)
			else
				DrawSprite(5)
			end if
			break

		end switch
	else
		switch Object.State
		case TAGATAGA_SET_LAUNCH_SPEED
		case TAGATAGA_EMERGE
			DrawSprite(6)
			break

		case TAGATAGA_MOVE_PINCH_1
			DrawSprite(7)
			break

		case TAGATAGA_TURN_1
			DrawSprite(8)
			break

		case TAGATAGA_TURN_2
			DrawSprite(9)
			break

		end switch
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")
	
	// New TagaTaga
	SpriteFrame(-16, -22, 32, 44, 223, 154)		// #0 - TagaTaga emerge Frame 0
	SpriteFrame(-16, -22, 32, 44, 223, 199)		// #1 - TagaTaga emerge Frame 1
	SpriteFrame(-16, -16, 32, 32, 35, 109)		// #2 - TagaTaga turning Frame 0
	SpriteFrame(-16, -16, 32, 32, 68, 109)		// #3 - TagaTaga turning Frame 1
	SpriteFrame(-16, -22, 32, 44, 1, 101)		// #4 - TagaTaga submerge Frame 0
	SpriteFrame(-16, -22, 32, 44, 1, 146)		// #5 - TagaTaga submerge Frame 1
	// Old TagaTaga
	SpriteFrame(-16, -22, 32, 44, 223, 154)		// #6 - TagaTaga emerge Frame 0
	SpriteFrame(-16, -16, 32, 32, 35, 109)		// #7 - TagaTaga turning Frame 0
	SpriteFrame(-16, -16, 32, 32, 35, 142)		// #8 - TagaTaga turning Frame 1
	SpriteFrame(-16, -22, 32, 44, 1, 191)		// #9 - TagaTaga submerge Frame 0

	// Used to be below LoadSpriteSheet, moved here for consistency
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[TagaTaga]
			Object[ArrayPos0].YOriginPos = Object[ArrayPos0].YPos
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.Quality
			CheckResult &= 1
			break
		case 0 // type
			CheckResult = Object.Quality
			CheckResult &= 1
			break
		end switch
	else
		switch Editor.variableID
		case EDIT_VAR_PROPVAL // property value
			Object.Quality = Editor.VariableValue
			Object.Quality &= 1
			break
		case 0 // type
			Object.Quality = Editor.VariableValue
			Object.Quality &= 1
			break
		end switch
	end if
end sub

sub RSDKDraw
	DrawSprite(Object.Quality)
end sub

sub RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")
	
	// New TagaTaga
	SpriteFrame(-16, -22, 32, 44, 223, 154)		// #0 - TagaTaga emerge Frame 0
	// Old TagaTaga
	SpriteFrame(-16, -22, 32, 44, 223, 154)		// #6 - TagaTaga emerge Frame 0


	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("New", 0)
	AddEnumVariable("Ruined", 1)
end sub
