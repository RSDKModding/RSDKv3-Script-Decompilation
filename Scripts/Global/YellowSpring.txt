//------------Sonic CD Yellow Spring Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.ActiveSpring
#alias Object.Value7	:	Object.YFlipped	// Only flipped in functionality, not in the way it displays
#alias 2	:	FLIP_Y

// Player Aliases
#alias Player.Value7:Player.AnimationCheck

#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

#alias 0	:	CMODE_FLOOR
#alias 2	:	CMODE_ROOF

#alias 11	:	ANI_BOUNCING

// Property Values
#alias 0	: SPRING_FACING_UP
#alias 1	: SPRING_FACING_RIGHT
#alias 2	: SPRING_FACING_LEFT
#alias 3	: SPRING_FACING_DOWN
#alias 4	: SPRING_FACING_DIAGONAL_UR
#alias 5	: SPRING_FACING_DIAGONAL_UL
#alias 6	: SPRING_FACING_DIAGONAL_DR	// Not actually facing down
#alias 7	: SPRING_FACING_DIAGONAL_DL	// Not actually facing down


sub ObjectPlayerInteraction
	switch Object.PropertyValue
	case SPRING_FACING_UP
		TempValue0 = false
		if Player.Gravity == GRAVITY_AIR
			TempValue0 = true
		end if
		if Player.CollisionMode > CMODE_FLOOR
			if Player.YVelocity < 0
				TempValue0 = true
			end if
		end if

		if TempValue0 == false
			PlayerObjectCollision(C_BOX, -16, -8, 16, 8)
			PlayerObjectCollision(C_TOUCH, -15, -10, 15, -6)
			if CheckResult == true
				Player.AnimationCheck = ANI_WALKING
				if Player.Animation == ANI_RUNNING
					Player.AnimationCheck = ANI_RUNNING
				end if
				if Player.Animation == ANI_PEELOUT
					Player.AnimationCheck = ANI_PEELOUT
				end if

				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.YVelocity = -655360	// 0xA0000
				Player.Animation = ANI_BOUNCING
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		else
			if Player.YVelocity >= 0
				PlayerObjectCollision(C_PLATFORM, -16, -8, 16, 8)
				PlayerObjectCollision(C_TOUCH, -15, -10, 15, -6)
				if CheckResult == true
					Player.AnimationCheck = ANI_WALKING
					if Player.Animation == ANI_RUNNING
						Player.AnimationCheck = ANI_RUNNING
					end if
					if Player.Animation == ANI_PEELOUT
						Player.AnimationCheck = ANI_PEELOUT
					end if
					
					Object.ActiveSpring = true
					Player.State = PlayerObject_HandleAir
					Player.Gravity = GRAVITY_AIR
					Player.YVelocity = -655360	// 0xA0000
					Player.Animation = ANI_BOUNCING
					Player.Timer = 0
					PlaySfx(11, 0)
				end if
			end if
		end if
		break

	case SPRING_FACING_RIGHT
		PlayerObjectCollision(C_BOX, -8, -16, 8, 16)
		if Player.Gravity == GRAVITY_GROUND
			PlayerObjectCollision(C_TOUCH, 6, -15, 11, 15)
			if CheckResult == true
				Object.ActiveSpring = true
				Player.Angle = 0
				Player.Speed = 655360			// 0xA0000
				Player.CollisionMode = CMODE_FLOOR
				Player.Pushing = false
				Player.Direction = FACING_RIGHT
				Player.ControlLock = 16
				PlaySfx(11, 0)
			end if
		end if
		break

	case SPRING_FACING_LEFT
		PlayerObjectCollision(C_BOX, -8, -16, 8, 16)
		if Player.Gravity == GRAVITY_GROUND
			PlayerObjectCollision(C_TOUCH, -10, -15, -6, 15)
			if CheckResult == true
				Object.ActiveSpring = true
				Player.Speed = -655360			// -0xA0000
				Player.CollisionMode = CMODE_FLOOR
				Player.Pushing = false
				Player.Direction = FACING_LEFT
				Player.ControlLock = 16
				PlaySfx(11, 0)
			end if
		end if
		break

	case SPRING_FACING_DOWN
		PlayerObjectCollision(C_BOX, -16, -8, 16, 8)
		PlayerObjectCollision(C_TOUCH, -15, 6, 15, 10)
		if CheckResult == true
			Object.ActiveSpring = true
			if Player.CollisionMode == CMODE_ROOF
				FlipSign(Player.Speed)
				FlipSign(Player.XVelocity)
			end if
			Player.State = PlayerObject_HandleAir
			Player.Gravity = GRAVITY_AIR
			Player.YVelocity = 1048576		// 0x100000
			Player.Timer = 0
			PlaySfx(11, 0)
		end if
		break
	case SPRING_FACING_DIAGONAL_UR
		PlayerObjectCollision(C_TOUCH, -12, -16, 12, 12)
		if Player.Gravity == GRAVITY_GROUND
			if CheckResult == true
				Player.Animation = ANI_WALKING
				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.Speed = 475136		// 0x74000
				Player.YVelocity = -475136	// -0x74000
				Player.Direction = FACING_RIGHT
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		end if
		if Player.YVelocity >= 0
			if CheckResult == true
				Player.Animation = ANI_WALKING
				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.Speed = 475136		// 0x74000
				Player.YVelocity = -475136	// -0x74000
				Player.Direction = FACING_RIGHT
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		end if
		break

	case SPRING_FACING_DIAGONAL_UL
		PlayerObjectCollision(C_TOUCH, -12, -16, 12, 12)
		if Player.YVelocity >= 0
			if CheckResult == true
				Player.Animation = ANI_WALKING
				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.Speed = -475136		// -0x74000
				Player.YVelocity = -475136		// -0x74000
				Player.Direction = FACING_LEFT
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		end if
		if Player.Gravity == GRAVITY_GROUND
			if CheckResult == true
				Player.Animation = ANI_WALKING
				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.Speed = -475136		// -0x74000
				Player.YVelocity = -475136		// -0x74000
				Player.Direction = FACING_LEFT
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		end if
		break

	case SPRING_FACING_DIAGONAL_DR
		PlayerObjectCollision(C_TOUCH, -12, -12, 12, 12)
		if CheckResult == true
			Object.ActiveSpring = true
			Player.State = PlayerObject_HandleAir
			Player.Gravity = GRAVITY_AIR
			Player.Speed = 475136		// 0x74000
			Player.YVelocity = 475136	// 0x74000
			Player.Timer = 0
			PlaySfx(11, 0)
		end if
		break
		
	case SPRING_FACING_DIAGONAL_DL
		PlayerObjectCollision(C_TOUCH, -12, -12, 12, 12)
		if CheckResult == true
			Object.ActiveSpring = true
			Player.State = PlayerObject_HandleAir
			Player.Gravity = GRAVITY_AIR
			Player.Speed = -475136		// -0x74000
			Player.YVelocity = 475136	// -0x74000
			Player.Timer = 0
			PlaySfx(11, 0)
		end if
		break
	end switch

#platform: Use_Haptics
	if Object.ActiveSpring == true
		HapticEffect(9, 0, 0, 0)
	end if
#endplatform
end sub


sub ObjectDraw
	if Object.ActiveSpring == false
		DrawSpriteFX(Object.PropertyValue, FX_FLIP, Object.XPos, Object.YPos)
	else
		Object.ActiveSpring++
		TempValue0 = Object.ActiveSpring
		TempValue0 >>= 1
		switch TempValue0
		case 0
		case 1
			DrawSpriteFX(Object.PropertyValue, FX_FLIP, Object.XPos, Object.YPos)
			break

		case 2
		case 6
			TempValue1 = Object.PropertyValue
			TempValue1 += 8
			DrawSpriteFX(TempValue1, FX_FLIP, Object.XPos, Object.YPos)
			break

		case 3
		case 4
		case 5
			TempValue1 = Object.PropertyValue
			TempValue1 += 16
			DrawSpriteFX(TempValue1, FX_FLIP, Object.XPos, Object.YPos)
			break

		end switch

		if Object.ActiveSpring > 12
			Object.ActiveSpring = false
		end if
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("Global/Items.gif")

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[YellowSpring]
			if Object[ArrayPos0].PropertyValue > SPRING_FACING_DIAGONAL_UL
				Object[ArrayPos0].Direction = FLIP_Y
			end if
		end if
		ArrayPos0++
	loop

	// Springs
	SpriteFrame(-16, -8, 32, 16, 84, 183)		// #0  - Yellow Spring facing up
	SpriteFrame(-8, -16, 16, 32, 215, 116)		// #1  - Yellow Spring facing right
	SpriteFrame(-8, -16, 16, 32, 232, 116)		// #2  - Yellow Spring facing left
	SpriteFrame(-16, -8, 32, 16, 184, 239)		// #3  - Yellow Spring facing down
	SpriteFrame(-16, -16, 32, 32, 183, 165)		// #4  - Yellow Spring facing diagonal right
	SpriteFrame(-16, -16, 32, 32, 216, 182)		// #5  - Yellow Spring facing diagonal left
	SpriteFrame(-16, -16, 32, 32, 183, 165)		// #6  - Yellow Spring facing diagonal right 2
	SpriteFrame(-16, -16, 32, 32, 216, 182)		// #7  - Yellow Spring facing diagonal left 2

	// Spring Recoil
	SpriteFrame(-16, 0, 32, 8, 84, 183)			// #8  - Yellow Spring Head facing up
	SpriteFrame(-8, -16, 8, 32, 223, 116)		// #9  - Yellow Spring Head facing right
	SpriteFrame(0, -16, 8, 32, 232, 116)		// #10 - Yellow Spring Head facing left
	SpriteFrame(-16, -8, 32, 8, 215, 107)		// #11 - Yellow Spring Head facing down
	SpriteFrame(-16, -16, 32, 32, 216, 149)		// #12 - Yellow Spring Recoiled diagonal right
	SpriteFrame(-16, -16, 32, 32, 218, 215)		// #13 - Yellow Spring Recoiled diagonal left
	SpriteFrame(-16, -16, 32, 32, 216, 149)		// #14 - Yellow Spring Recoiled diagonal right 2
	SpriteFrame(-16, -16, 32, 32, 218, 215)		// #15 - Yellow Spring Recoiled diagonal left 2
	// Spring Launch
	SpriteFrame(-16, -32, 32, 40, 215, 34)		// #16 - Yellow Spring Launch facing up
	SpriteFrame(-8, -16, 40, 32, 134, 198)		// #17 - Yellow Spring Launch facing right
	SpriteFrame(-16, -16, 40, 32, 175, 198)		// #18 - Yellow Spring Launch facing left
	SpriteFrame(-16, -8, 32, 40, 215, 75)		// #19 - Yellow Spring Launch facing down
	SpriteFrame(-16, -32, 48, 48, 117, 83)		// #20 - Yellow Spring Launch diagonal right
	SpriteFrame(-32, -32, 48, 48, 166, 83)		// #21 - Yellow Spring Launch diagonal left
	SpriteFrame(-16, -32, 48, 48, 117, 83)		// #22 - Yellow Spring Launch diagonal right 2
	SpriteFrame(-32, -32, 48, 48, 166, 83)		// #23 - Yellow Spring Launch diagonal left 2
end sub

// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.PropertyValue
			break
		case 0 // type
			CheckResult = Object.PropertyValue
			CheckResult &= 7
			break
		end switch
	else
		switch Editor.variableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // type
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 7
			break
		end switch
	end if
end sub

sub RSDKDraw
	Object.Direction = 0
	if Object.PropertyValue > SPRING_FACING_DIAGONAL_UL
		Object.Direction = 2
	end if

	DrawSpriteFX(Object.PropertyValue, FX_FLIP, Object.XPos, Object.YPos)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Items.gif")
	
	// Springs
	SpriteFrame(-16, -8, 32, 16, 84, 183)		// #0  - Yellow Spring facing up
	SpriteFrame(-8, -16, 16, 32, 215, 116)		// #1  - Yellow Spring facing right
	SpriteFrame(-8, -16, 16, 32, 232, 116)		// #2  - Yellow Spring facing left
	SpriteFrame(-16, -8, 32, 16, 184, 239)		// #3  - Yellow Spring facing down
	SpriteFrame(-16, -16, 32, 32, 183, 165)		// #4  - Yellow Spring facing diagonal right
	SpriteFrame(-16, -16, 32, 32, 216, 182)		// #5  - Yellow Spring facing diagonal left
	SpriteFrame(-16, -16, 32, 32, 183, 165)		// #6  - Yellow Spring facing diagonal right 2
	SpriteFrame(-16, -16, 32, 32, 216, 182)		// #7  - Yellow Spring facing diagonal left 2

	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Up", 0)
	AddEnumVariable("Right", 1)
	AddEnumVariable("Left", 2)
	AddEnumVariable("Down", 3)
	AddEnumVariable("Diagonal (Up Right)", 4)
	AddEnumVariable("Diagonal (Up Left)", 5)
	AddEnumVariable("Diagonal (Down Right)", 6)
	AddEnumVariable("Diagonal (Down Right)", 7)
end sub
