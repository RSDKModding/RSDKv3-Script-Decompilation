//------------Sonic CD Spikes Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias 21: TYPE_SPIKES
#alias Player.Value4:Player.Invincible

// Property Values
#alias 0	:	SPIKES_FACING_UP
#alias 1	:	SPIKES_FACING_RIGHT
#alias 2	:	SPIKES_FACING_LEFT
#alias 3	:	SPIKES_FACING_DOWN

sub ObjectPlayerInteraction
	switch Object.PropertyValue
	case SPIKES_FACING_UP
		PlayerObjectCollision(C_BOX, -16, -12, 16, 16)
		if Player.YVelocity > -1
			if Player.Invincible == 0
				PlayerObjectCollision(C_TOUCH, -15, -13, 15, -12)
				if CheckResult == true
					Player.State = PlayerObject_Hurt
					if Player.XPos > Object.XPos
						Player.Speed = 131072	// 0x20000
					else
						Player.Speed = -131072	// -0x20000
					end if

				end if

			end if
		end if
		break

	case SPIKES_FACING_RIGHT
		PlayerObjectCollision(C_BOX, -16, -16, 15, 16)
		if Player.XVelocity <= 0
			if Player.Invincible == 0
				PlayerObjectCollision(C_TOUCH, 12, -15, 16, 15)
				if CheckResult == true
					Player.State = PlayerObject_Hurt
					Player.Speed = 131072	// 0x20000
				end if
			end if
		end if
		break

	case SPIKES_FACING_LEFT
		PlayerObjectCollision(C_BOX, -15, -16, 16, 16)
		if Player.Invincible == 0
			PlayerObjectCollision(C_TOUCH, -16, -15, -12, 15)
			if CheckResult == true
				Player.State = PlayerObject_Hurt
				Player.Speed = -131072		// -0x20000
			end if
		end if
		break

	case SPIKES_FACING_DOWN
		TempValue0 = Player.YVelocity
		PlayerObjectCollision(C_BOX, -16, -16, 16, 15)
		if TempValue0 < 1
			if Player.Invincible == 0
				PlayerObjectCollision(C_TOUCH, -15, 12, 15, 16)
				if CheckResult == true
					Player.State = PlayerObject_Hurt
					if Player.XPos > Object.XPos
						Player.Speed = 131072	// 0x20000
					else
						Player.Speed = -131072	// -0x20000
					end if
				end if
			end if
		end if
		break

	end switch
end sub


sub ObjectDraw
	DrawSprite(Object.PropertyValue)
end sub


sub ObjectStartup
	LoadSpriteSheet("Global/Items3.gif")

	SpriteFrame(-16, -16, 32, 32, 50, 1)	// #0 - Spikes facing up
	SpriteFrame(-16, -16, 32, 32, 50, 34)	// #1 - Spikes facing right
	SpriteFrame(-16, -16, 32, 32, 50, 67)	// #2 - Spikes facing left
	SpriteFrame(-16, -16, 32, 32, 50, 100)	// #3 - Spikes facing down
end sub

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.PropertyValue
			break
		case 0 // direction
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // direction
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items3.gif")
	SpriteFrame(-16, -16, 32, 32, 50, 1)
	SpriteFrame(-16, -16, 32, 32, 50, 34)
	SpriteFrame(-16, -16, 32, 32, 50, 67)
	SpriteFrame(-16, -16, 32, 32, 50, 100)

	AddEditorVariable("direction")
	SetActiveVariable("direction")
	AddEnumVariable("Upwards", 0)
	AddEnumVariable("Right", 1)
	AddEnumVariable("Left", 2)
	AddEnumVariable("Down", 3)
end sub
