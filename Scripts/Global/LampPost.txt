//------------Sonic CD LampPost Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias 15: TYPE_LAMPPOST
#alias Object.Value0	:	Object.Timer

// States
#alias 0				:	LAMPPOST_IDLE
#alias 1				:	LAMPPOST_ROTATE
#alias 2				:	LAMPPOST_USED

sub ObjectPlayerInteraction
	if Object.State == LAMPPOST_IDLE

		PlayerObjectCollision(C_TOUCH, -8, -32, 8, 32)
		if CheckResult == true
			LampPost.Check = Object.EntityNo

			if Player.XPos < Object.XPos
				LampPost.Side = -1572864	//-0x180000

#platform: Mobile			  
				ObjectTileCollision(2, -24, 0, 0)	// Code to avoid getting stuck in a wall i guess?
				if CheckResult == true
					LampPost.Side = 0
				end if
#endplatform

			else
				LampPost.Side = 1572864		//0x180000

#platform: Mobile
				ObjectTileCollision(1, 24, 0, 0)	// Code to avoid getting stuck in a wall i guess?
				if CheckResult == true
					LampPost.Side = 0
				end if
#endplatform

			end if
			// Save Time
			Rec_Milliseconds = Stage.MilliSeconds
			Rec_Seconds = Stage.Seconds
			Rec_Minutes = Stage.Minutes

			Object.State = LAMPPOST_ROTATE
			Object.Timer = 384
			PlaySfx(9, 0)

#platform: Use_Haptics
			HapticEffect(20, 0, 0, 0)
#endplatform		 
		end if

	end if
end sub


sub ObjectDraw
	switch Object.State
	case LAMPPOST_IDLE
		DrawSprite(0)
		break

	case LAMPPOST_ROTATE
		DrawSprite(1)
		Cos(TempValue0, Object.Timer)
		TempValue0 <<= 10
		Sin(TempValue1, Object.Timer)
		TempValue1 <<= 10
		TempValue0 += Object.XPos
		TempValue1 += Object.YPos
		TempValue1 -= 1572864		//0x180000
		DrawSpriteXY(2, TempValue0, TempValue1)
		Object.Timer += 16
		if Object.Timer > 896
			Object.Timer = 0
			Object.State = 2
		end if
		break

	case LAMPPOST_USED
		DrawSprite(1)
		Object.Timer++
		if Object.Timer == 8
			Object.Timer = 0
		end if
		TempValue0 = Object.YPos
		TempValue0 -= 2097152		//0x200000
		if Object.Timer > 3
			DrawSpriteXY(4, Object.XPos, TempValue0)
		else
			DrawSpriteXY(3, Object.XPos, TempValue0)
		end if
		break
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("Global/Items.gif")
	SpriteFrame(-8, -40, 16, 64, 1, 137)	// #0 - Lamp Post Idle
	SpriteFrame(-8, -24, 16, 48, 1, 153)	// #1 - Lamp Post Body
	SpriteFrame(-8, -8, 16, 16, 1, 137)		// #2 - Lamp Post Blue Head
	SpriteFrame(-8, -8, 16, 16, 1, 236)		// #3 - Lamp Post Yellow Head
	SpriteFrame(-8, -8, 16, 16, 18, 236)	// #4 - Lamp Post Red Head
	if Warp.XPos == 0
		if Object[24].PropertyValue == 0
			if LampPost.Check > 31
				ArrayPos0 = LampPost.Check
				Player.XPos = Object[ArrayPos0].XPos
				Player.XPos += LampPost.Side
				Player.YPos = Object[ArrayPos0].YPos
				Object[ArrayPos0].State = 2
				Stage.MilliSeconds = Rec_Milliseconds
				Stage.Seconds = Rec_Seconds
				Stage.Minutes = Rec_Minutes
			end if
		end if
	end if
end sub

sub RSDK
	LoadSpriteSheet("Global/Display.gif")
	SetEditorIcon(Icon0, SingleIcon, -16, -16, 32, 32, 1, 143)
end sub
