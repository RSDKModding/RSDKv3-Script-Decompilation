//----------------Sonic CD Warp Sonic Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.FrameTimer
#alias Object.Value1	:	Object.Timer
#alias Object.Value2	:	Object.WarpStarTimer

// Afterimage 1 Aliases
#alias Object[1].Value0	:	AfterImage1.Delay
#alias Object[1].Value1	:	AfterImage1.Delay2
#alias Object[1].Value2	:	AfterImage1.Delay3
#alias Object[1].Value3	:	AfterImage1.YPos

// Afterimage 2 Aliases
#alias Object[1].Value4	:	AfterImage2.Delay
#alias Object[1].Value5	:	AfterImage2.Delay2
#alias Object[1].Value6	:	AfterImage2.Delay3
#alias Object[1].Value7	:	AfterImage2.YPos

// Afterimage 3 Aliases
#alias Object[2].Value0	:	AfterImage3.Delay
#alias Object[2].Value1	:	AfterImage3.Delay2
#alias Object[2].Value2	:	AfterImage3.Delay3
#alias Object[2].Value3	:	AfterImage3.YPos

// Time Warp Aliases
#alias Object[0].State	:	TimeWarp.State

// Warp Star Aliases
#alias Object.Value1	:	WarpStar.RandFadeTimer
#alias Object.State		:	WarpStar.State

// States
#alias 0	:	WARPSONIC_REACH_CENTER
#alias 1	:	WARPSONIC_IDLE
#alias 2	:	WARPSONIC_LEAVE

// WarpStar States
#alias 2	:	WARPSTAR_CUTSCENE

// TimeWarp States
#alias 4	:	TIMEWARP_LEAVING

// Player
#alias 0	:	PLAYER_SONIC_A
#alias 1	:	PLAYER_TAILS_A

// Ink
#alias 2	:	INK_ALPHA


sub ObjectMain
	// tldr;
	// this gets the ypos for the afterimages
	AfterImage3.YPos   = AfterImage3.Delay3
	AfterImage3.Delay3 = AfterImage3.Delay2
	AfterImage3.Delay2 = AfterImage3.Delay
	AfterImage3.Delay  = AfterImage2.YPos

	AfterImage2.YPos   = AfterImage2.Delay3
	AfterImage2.Delay3 = AfterImage2.Delay2
	AfterImage2.Delay2 = AfterImage2.Delay
	AfterImage2.Delay  = AfterImage1.YPos

	AfterImage1.YPos   = AfterImage1.Delay3
	AfterImage1.Delay3 = AfterImage1.Delay2
	AfterImage1.Delay2 = AfterImage1.Delay
	AfterImage1.Delay  = Object.YPos

	switch Object.State
	case WARPSONIC_REACH_CENTER
		Object.YPos -= 0x80000
		if Object.YPos < 0x780000
			Object.YPos  = 0x780000
			Object.State = WARPSONIC_IDLE
			Object.Timer = 0
		end if
		break

	case WARPSONIC_IDLE
		Object.Timer++
				
#platform: Use_Haptics
		if Object.Timer == 60
			HapticEffect(9, 0, 0, 0)
		end if
#endplatform

		if Object.Timer == 120
			Object.Timer = 0
			Object.State = WARPSONIC_LEAVE
			TimeWarp.State = TIMEWARP_LEAVING
		end if
		break

	case WARPSONIC_LEAVE
		Object.YPos -= 0x80000
		if Object.OutOfBounds == true
			Object.Type = TypeName[Blank Object]
		end if
		break

	end switch

	Object.WarpStarTimer++
	if Object.WarpStarTimer == 4
		Object.WarpStarTimer = 0
		Rand(TempValue0, 32)
		TempValue0  -= 16
		TempValue0 <<= 16
		TempValue0  += Object.XPos

		Rand(TempValue1, 32)
		TempValue1  -= 16
		TempValue1 <<= 16
		TempValue1  += Object.YPos

		CreateTempObject(TypeName[Warp Star], 0, TempValue0, TempValue1)
		WarpStar[TempObjectPos].State = WARPSTAR_CUTSCENE

		Rand(WarpStar[TempObjectPos].RandFadeTimer, 32)
		WarpStar[TempObjectPos].RandFadeTimer += 16
	end if
end sub


sub ObjectDraw
	// Afterimages Frames
	TempValue0 = Object.Frame
	TempValue0++
	if TempValue0 > 4
		TempValue0 = 0
	end if

	// Afterimages
	if Object.YPos < 0x1040000
		Object.InkEffect = INK_ALPHA
		Object.Alpha = 160
		DrawSpriteFX(TempValue0, FX_INK, Object.XPos, AfterImage1.YPos)
		Object.Alpha = 128
		DrawSpriteFX(TempValue0, FX_INK, Object.XPos, AfterImage2.YPos)
		Object.Alpha = 96
		DrawSpriteFX(TempValue0, FX_INK, Object.XPos, AfterImage3.YPos)
	end if

	DrawSprite(Object.Frame)
	Object.FrameTimer++
	if Object.FrameTimer > 2
		Object.FrameTimer = 0
		Object.Frame++
		if Object.Frame > 4
			Object.Frame = 0
		end if
	end if
end sub


sub ObjectStartup
	// #0 - Bouncing Frame 0
	// #1 - Bouncing Frame 1
	// #2 - Bouncing Frame 2
	// #3 - Bouncing Frame 3
	// #4 - Bouncing Frame 4

	if Stage.PlayerListPos == PLAYER_SONIC_A
		LoadSpriteSheet("Global/Items3.gif")
		SpriteFrame(-14, -22, 29, 44, 116, 211)
		SpriteFrame(-11, -22, 20, 44, 146, 211)
		SpriteFrame(-11, -22, 25, 44, 167, 211)
		SpriteFrame(-13, -22, 25, 44, 193, 211)
		SpriteFrame(-8, -22, 20, 44, 219, 211)
	end if

	if Stage.PlayerListPos == PLAYER_TAILS_A
		LoadSpriteSheet("Players/Tails2.gif")
		SpriteFrame(-17, -22, 24, 48, 1, 1)
		SpriteFrame(-11, -22, 24, 48, 26, 1)
		SpriteFrame(-13, -22, 24, 48, 51, 1)
		SpriteFrame(-7, -22, 24, 48, 76, 1)
		SpriteFrame(-10, -22, 21, 48, 101, 1)
	end if

#platform: Use_Origins
	if Stage.PlayerListPos == PLAYER_KNUCKLES
		LoadSpriteSheet("Players/KTE5.gif")
		SpriteFrame(-14, -21, 29, 45, 1, 70)
		SpriteFrame(-13, -21, 24, 45, 31, 70)
		SpriteFrame(-11, -21, 30, 45, 56, 70)
		SpriteFrame(-18, -21, 30, 45, 87, 66)
		SpriteFrame(-10, -21, 24, 45, 118, 66)
	end if
	
	if Stage.PlayerListPos == PLAYER_AMY
		LoadSpriteSheet("Players/Amy3.gif")
		SpriteFrame(-12, -21, 24, 39, 1, 1)
		SpriteFrame(-13, -21, 21, 39, 26, 1)
		SpriteFrame(-14, -21, 28, 39, 48, 1)
		SpriteFrame(-13, -21, 28, 39, 77, 1)
		SpriteFrame(-7, -21, 21, 39, 1, 41)
	end if
#endplatform
	
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items3.gif")
	SpriteFrame(-14, -22, 29, 44, 116, 211)	// #0 - Bouncing Frame 0

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
