//------------Sonic CD Red Spring Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.ActiveSpring
#alias Object.Value7	:	Object.YFlipped	// Only flipped in functionality, not in the way it displays

// Directions
#alias 2	:	FACING_UPWARDS

// Player Aliases
#alias Player.Value7:Player.AnimationCheck
#alias 11	:	ANI_BOUNCING

// Gravity
#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

// Collision Modes
#alias 0	:	CMODE_FLOOR
#alias 2	:	CMODE_ROOF

// Property Values
#alias 0	: SPRING_FACING_UP
#alias 1	: SPRING_FACING_RIGHT
#alias 2	: SPRING_FACING_LEFT
#alias 3	: SPRING_FACING_DOWN
#alias 4	: SPRING_FACING_DIAGONAL_UR
#alias 5	: SPRING_FACING_DIAGONAL_UL
#alias 6	: SPRING_FACING_DIAGONAL_DR	// Actually not facing down
#alias 7	: SPRING_FACING_DIAGONAL_DL	// Actually not facing down


sub ObjectPlayerInteraction
	switch Object.PropertyValue
	case SPRING_FACING_UP
		TempValue0 = false
		if Player.Gravity == GRAVITY_AIR
			TempValue0 = true
		end if
		if Player.CollisionMode > CMODE_FLOOR
			if Player.YVelocity < 0
				TempValue0 = true
			end if
		end if

		if TempValue0 == false
			PlayerObjectCollision(C_BOX, -16, -8, 16, 8)
			PlayerObjectCollision(C_TOUCH, -15, -10, 15, -6)
			if CheckResult == true
				Player.AnimationCheck = ANI_WALKING
				if Player.Animation == ANI_RUNNING
					Player.AnimationCheck = ANI_RUNNING
				end if
				if Player.Animation == ANI_PEELOUT
					Player.AnimationCheck = ANI_PEELOUT
				end if

				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.YVelocity = -1048576
				Player.Animation = ANI_BOUNCING
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		else
			if Player.YVelocity >= 0
				PlayerObjectCollision(C_PLATFORM, -16, -8, 16, 8)
				PlayerObjectCollision(C_TOUCH, -15, -10, 15, -6)
				if CheckResult == true
					Player.AnimationCheck = ANI_WALKING
					if Player.Animation == ANI_RUNNING
						Player.AnimationCheck = ANI_RUNNING
					end if
					if Player.Animation == ANI_PEELOUT
						Player.AnimationCheck = ANI_PEELOUT
					end if
					
					Object.ActiveSpring = true
					Player.State = PlayerObject_HandleAir
					Player.Gravity = GRAVITY_AIR
					Player.YVelocity = -1048576	// -0x100000
					Player.Animation = ANI_BOUNCING
					Player.Timer = 0
					PlaySfx(11, 0)
				end if
			end if
		end if
		break

	case SPRING_FACING_RIGHT
		PlayerObjectCollision(C_BOX, -8, -16, 8, 16)
		if Player.Gravity == GRAVITY_GROUND
			PlayerObjectCollision(C_TOUCH, 6, -15, 11, 15)
			if CheckResult == true
				Object.ActiveSpring = true
				Player.Angle = 0
				Player.Speed = 1048576			// 0x100000
				Player.CollisionMode = CMODE_FLOOR
				Player.Pushing = false
				Player.Direction = FACING_RIGHT
				Player.ControlLock = 16
				PlaySfx(11, 0)
			end if
		else
			if Object.YFlipped == true
				PlayerObjectCollision(C_TOUCH, 6, -15, 11, 15)
				if CheckResult == true
					Object.ActiveSpring = true
					Player.Angle = 0
					Player.Speed = 1048576		// 0x100000
					Player.CollisionMode = CMODE_FLOOR
					Player.Pushing = false
					Player.Direction = FACING_RIGHT
					Player.ControlLock = 16
					PlaySfx(11, 0)
				end if
			end if
		end if
		break

	case SPRING_FACING_LEFT
		PlayerObjectCollision(C_BOX, -8, -16, 8, 16)
		if Player.Gravity == GRAVITY_GROUND
			PlayerObjectCollision(C_TOUCH, -10, -15, -6, 15)
			if CheckResult == true
				Object.ActiveSpring = true
				Player.Speed = -1048576			// -0x100000
				Player.CollisionMode = CMODE_FLOOR
				Player.Pushing = false
				Player.Direction = FACING_LEFT
				Player.ControlLock = 16
				PlaySfx(11, 0)
			end if
		else
			if Object.YFlipped == true
				PlayerObjectCollision(C_TOUCH, -10, -15, -6, 15)
				if CheckResult == true
					Object.ActiveSpring = true
					Player.Speed = -1048576		// -0x100000
					Player.YVelocity = 131072	// 0x20000
					Player.CollisionMode = CMODE_FLOOR
					Player.Pushing = false
					Player.Direction = FACING_LEFT
					Player.ControlLock = 16
					PlaySfx(11, 0)
				end if
			end if
		end if
		break

	case SPRING_FACING_DOWN
		PlayerObjectCollision(C_BOX, -16, -8, 16, 8)
		PlayerObjectCollision(C_TOUCH, -15, 6, 15, 10)
		if CheckResult == true
			if Player.CollisionMode == CMODE_ROOF
				FlipSign(Player.Speed)
				FlipSign(Player.XVelocity)
			end if
			Object.ActiveSpring = true
			Player.State = PlayerObject_HandleAir
			Player.Gravity = GRAVITY_AIR
			Player.YVelocity = 1048576		// 0x100000
			Player.Timer = 0
			PlaySfx(11, 0)
		end if
		break

	case SPRING_FACING_DIAGONAL_UR
		PlayerObjectCollision(C_TOUCH, -12, -16, 12, 12)
		if Player.Gravity == GRAVITY_GROUND
			if CheckResult == true
				Player.Animation = ANI_WALKING
				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.Speed = 737280		// 0xB4000
				Player.YVelocity = -737280	// -0xB4000
				Player.Direction = FACING_RIGHT
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		end if
		if Player.YVelocity >= 0
			if CheckResult == true
				Player.Animation = ANI_WALKING
				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.Speed = 737280		// 0xB4000
				Player.YVelocity = -737280	// -0xB4000
				Player.Direction = FACING_RIGHT
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		end if
		break

	case SPRING_FACING_DIAGONAL_UL
		PlayerObjectCollision(C_TOUCH, -12, -16, 12, 12)
		if Player.YVelocity >= 0
			if CheckResult == true
				Player.Animation = ANI_WALKING
				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.Speed = -737280		// -0xB4000
				Player.YVelocity = -737280	// -0xB4000
				Player.Direction = FACING_LEFT
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		end if
		if Player.Gravity == GRAVITY_GROUND
			if CheckResult == true
				Player.Animation = ANI_WALKING
				Object.ActiveSpring = true
				Player.State = PlayerObject_HandleAir
				Player.Gravity = GRAVITY_AIR
				Player.Speed = -737280		// -0xB4000
				Player.YVelocity = -737280	// -0xB4000
				Player.Direction = FACING_LEFT
				Player.Timer = 0
				PlaySfx(11, 0)
			end if
		end if
		break

	case SPRING_FACING_DIAGONAL_DR
		PlayerObjectCollision(C_TOUCH, -12, -12, 12, 12)
		if CheckResult == true
			Object.ActiveSpring = true
			Player.State = PlayerObject_HandleAir
			Player.Gravity = GRAVITY_AIR
			Player.Speed = 737280		// 0xB4000
			Player.YVelocity = 737280	// 0xB4000
			Player.Timer = 0
			PlaySfx(11, 0)
		end if
		break

	case SPRING_FACING_DIAGONAL_DL
		PlayerObjectCollision(C_TOUCH, -12, -12, 12, 12)
		if CheckResult == true
			Object.ActiveSpring = true
			Player.State = PlayerObject_HandleAir
			Player.Gravity = GRAVITY_AIR
			Player.Speed = -737280		// -0xB4000
			Player.YVelocity = 737280	// 0xB4000
			Player.Timer = 0
			PlaySfx(11, 0)
		end if
		break
	end switch

#platform: Use_Haptics
	if Object.ActiveSpring == true
		HapticEffect(9, 0, 0, 0)
	end if
#endplatform
end sub


sub ObjectDraw
	if Object.ActiveSpring == false
		DrawSpriteFX(Object.PropertyValue, FX_FLIP, Object.XPos, Object.YPos)
	else
		Object.ActiveSpring++
		TempValue0 = Object.ActiveSpring
		TempValue0 >>= 1
		switch TempValue0
		case 0
		case 1
			DrawSpriteFX(Object.PropertyValue, FX_FLIP, Object.XPos, Object.YPos)
			break

		case 2
		case 6
			TempValue1 = Object.PropertyValue
			TempValue1 += 8
			DrawSpriteFX(TempValue1, FX_FLIP, Object.XPos, Object.YPos)
			break

		case 3
		case 4
		case 5
			TempValue1 = Object.PropertyValue
			TempValue1 += 16
			DrawSpriteFX(TempValue1, FX_FLIP, Object.XPos, Object.YPos)
			break

		end switch

		if Object.ActiveSpring > 12
			Object.ActiveSpring = false
		end if
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("Global/Items.gif")

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[RedSpring]
			if Object[ArrayPos0].PropertyValue > SPRING_FACING_DIAGONAL_DL
				Object[ArrayPos0].PropertyValue -= 8
				Object[ArrayPos0].YFlipped = true
			end if
			if Object[ArrayPos0].PropertyValue > SPRING_FACING_DIAGONAL_UL
				Object[ArrayPos0].Direction = FACING_UPWARDS
			end if
		end if
		ArrayPos0++
	loop

	// Springs
	SpriteFrame(-16, -8, 32, 16, 84, 1)			// #0  - Red Spring facing up
	SpriteFrame(-8, -16, 16, 32, 117, 1)		// #1  - Red Spring facing right
	SpriteFrame(-8, -16, 16, 32, 175, 1)		// #2  - Red Spring facing left
	SpriteFrame(-16, -8, 32, 16, 84, 59)		// #3  - Red Spring facing down
	SpriteFrame(-16, -16, 32, 32, 84, 117)		// #4  - Red Spring facing diagonal right
	SpriteFrame(-16, -16, 32, 32, 117, 165)		// #5  - Red Spring facing diagonal left
	SpriteFrame(-16, -16, 32, 32, 84, 117)		// #6  - Red Spring facing diagonal right 2
	SpriteFrame(-16, -16, 32, 32, 117, 165)		// #7  - Red Spring facing diagonal left 2

	// Spring Recoil
	SpriteFrame(-16, 0, 32, 8, 84, 1)			// #8  - Red Spring Head facing up
	SpriteFrame(-8, -16, 8, 32, 125, 1)			// #9  - Red Spring Head facing right
	SpriteFrame(0, -16, 8, 32, 175, 1)			// #10 - Red Spring Head facing left
	SpriteFrame(-16, -8, 32, 8, 84, 67)			// #11 - Red Spring Head facing down
	SpriteFrame(-16, -16, 32, 32, 84, 150)		// #12 - Red Spring Recoiled diagonal right
	SpriteFrame(-16, -16, 32, 32, 150, 165)		// #13 - Red Spring Recoiled diagonal left
	SpriteFrame(-16, -16, 32, 32, 84, 150)		// #14 - Red Spring Recoiled diagonal right 2
	SpriteFrame(-16, -16, 32, 32, 150, 165)		// #15 - Red Spring Recoiled diagonal left 2
	// Spring Launch
	SpriteFrame(-16, -32, 32, 40, 84, 18)		// #16 - Red Spring Launch facing up
	SpriteFrame(-8, -16, 40, 32, 134, 1)		// #17 - Red Spring Launch facing right
	SpriteFrame(-16, -16, 40, 32, 192, 1)		// #18 - Red Spring Launch facing left
	SpriteFrame(-16, -8, 32, 40, 84, 76)		// #19 - Red Spring Launch facing down
	SpriteFrame(-16, -32, 48, 48, 117, 34)		// #20 - Red Spring Launch diagonal right
	SpriteFrame(-32, -32, 48, 48, 166, 34)		// #21 - Red Spring Launch diagonal left
	SpriteFrame(-16, -32, 48, 48, 117, 34)		// #22 - Red Spring Launch diagonal right 2
	SpriteFrame(-32, -32, 48, 48, 166, 34)		// #23 - Red Spring Launch diagonal left 2
end sub

// ========================
// Editor Subs
// ========================

sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.PropertyValue
			break
		case 0 // type
			CheckResult = Object.PropertyValue
			CheckResult &= 7
			break
		end switch
	else
		switch Editor.variableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // type
			Object.PropertyValue = Editor.VariableValue
			Object.PropertyValue &= 7
			break
		end switch
	end if
end sub

sub RSDKDraw
	Object.Direction = FACING_RIGHT
	if Object.PropertyValue > SPRING_FACING_DIAGONAL_UL
		Object.Direction = FACING_UPWARDS
	end if

	DrawSpriteFX(Object.PropertyValue, FX_FLIP, Object.XPos, Object.YPos)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Items.gif")
	
	// Springs
	SpriteFrame(-16, -8, 32, 16, 84, 1)			// #0  - Red Spring facing up
	SpriteFrame(-8, -16, 16, 32, 117, 1)		// #1  - Red Spring facing right
	SpriteFrame(-8, -16, 16, 32, 175, 1)		// #2  - Red Spring facing left
	SpriteFrame(-16, -8, 32, 16, 84, 59)		// #3  - Red Spring facing down
	SpriteFrame(-16, -16, 32, 32, 84, 117)		// #4  - Red Spring facing diagonal right
	SpriteFrame(-16, -16, 32, 32, 117, 165)		// #5  - Red Spring facing diagonal left
	SpriteFrame(-16, -16, 32, 32, 84, 117)		// #6  - Red Spring facing diagonal right 2
	SpriteFrame(-16, -16, 32, 32, 117, 165)		// #7  - Red Spring facing diagonal left 2

	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Up", 0)
	AddEnumVariable("Right", 1)
	AddEnumVariable("Left", 2)
	AddEnumVariable("Down", 3)
	AddEnumVariable("Diagonal (Up Right)", 4)
	AddEnumVariable("Diagonal (Up Left)", 5)
	AddEnumVariable("Diagonal (Down Right)", 6)
	AddEnumVariable("Diagonal (Down Right)", 7)
end sub
