//-------------------Sonic CD Ring Script---------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.PropertyValue	: Object.CollisionPlane

// Player Aliase
#alias Player.Value0		: Player.Rings

// Pan
#alias 0	:	PAN_RIGHT
#alias 1	:	PAN_LEFT

// GameModes
#alias 2	:	GAMEMODE_TIMEATTACK

// Property Values
#alias 0	:	PROPVAL_PLANEA


sub ObjectPlayerInteraction
	if Player.State != PlayerObject_Knockback

		if Player.CollisionPlane == Object.CollisionPlane

			PlayerObjectCollision(C_TOUCH, -8, -8, 8, 8)
			if CheckResult == true
				Object.Type = TypeName[Ring Sparkle]
				Player.Rings++
				if Player.Rings > 999
					Player.Rings = 999
				end if
				
				if Player.Rings >= Ring.ExtraLife
					if Options.GameMode != GAMEMODE_TIMEATTACK
						Player.Lives++
						if Stage.PlayerListPos == 0		// PlayerName[SONIC]
							PlaySfx(20, 0)
						else
							PlaySfx(21, 0)
						end if
					end if

					Ring.ExtraLife += 100

					// You can only achieve 3 extra lives via rings
					if Ring.ExtraLife > 300
						Ring.ExtraLife = 1000
					end if

					// King of the Rings achievement criteria
					if Player.Rings >= 200
						if Stage.PlayerListPos == 0
							if Stage.DebugMode == false
								SetAchievement(4, 100)
							end if
						end if
					end if
				end if

				if Ring.Pan == PAN_RIGHT
					SetSfxAttributes(1, -1, -100)
					Ring.Pan = PAN_LEFT
				else
					SetSfxAttributes(2, -1, 100)
					Ring.Pan = PAN_RIGHT
				end if
			end if

		end if

	end if
end sub


sub ObjectDraw
	DrawSprite(Ring.Frame)
end sub


sub ObjectStartup
	LoadSpriteSheet("Global/Items.gif")

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Ring]
			if Object[ArrayPos0].CollisionPlane == PROPVAL_PLANEA
				Object[ArrayPos0].DrawOrder = 4
			else
				Object[ArrayPos0].DrawOrder = 2
			end if
		end if
		ArrayPos0++
	loop

	SpriteFrame(-8, -8, 16, 16, 1, 1)		// #0 - Ring Frame 0
	SpriteFrame(-8, -8, 16, 16, 1, 18)		// #1 - Ring Frame 1
	SpriteFrame(-8, -8, 16, 16, 1, 35)		// #2 - Ring Frame 2
	SpriteFrame(-8, -8, 16, 16, 1, 52)		// #3 - Ring Frame 3
	SpriteFrame(-8, -8, 16, 16, 1, 69)		// #4 - Ring Frame 4
	SpriteFrame(-8, -8, 16, 16, 1, 86)		// #5 - Ring Frame 5
	SpriteFrame(-8, -8, 16, 16, 1, 103)		// #6 - Ring Frame 6
	SpriteFrame(-8, -8, 16, 16, 1, 120)		// #7 - Ring Frame 7
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Items.gif")
	SpriteFrame(-8, -8, 16, 16, 1, 1)		// #0 - Ring Frame 0

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
