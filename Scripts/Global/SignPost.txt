//------------Sonic CD SignPost Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias 18: TYPE_SIGNPOST
#alias Object.Value0	:	Object.Timer
#alias Object.Value1	:	Object.AnimationTimer
#alias 0	:	SIGNPOST_IDLE
#alias 1	:	SIGNPOST_FLIP
#alias 2	:	SIGNPOST_MOVE_PLAYER
#alias 3	:	SIGNPOST_STOP_PLAYER
#alias 4	:	SIGNPOST_BLANK

sub ObjectMain
	switch Object.State
	case SIGNPOST_IDLE
		Object.Frame = 1
		TempValue0 = Object.YPos
		TempValue0 += 6291456	// 0x600000
		if Player.YPos < TempValue0	// Makes sure the player triggers the sign post based purely on X position, no matter the height
			Stage.NewXBoundary1 = Object.XPos
			Stage.NewXBoundary1 >>= 16
			Stage.NewXBoundary1 -= Screen.CenterX
			Stage.NewXBoundary1 -= Screen.CenterX

			Stage.NewXBoundary2 = Object.XPos
			Stage.NewXBoundary2 >>= 16
			Stage.NewXBoundary2 += Screen.CenterX

			if Player.XPos > Object.XPos	// Check that the player crossed the sign post
				Stage.NewXBoundary1 = Object.XPos
				Stage.NewXBoundary1 >>= 16
				Stage.NewXBoundary1 -= Screen.CenterX

				Stage.TimeEnabled = false
				Object.State = SIGNPOST_FLIP
				Object.Timer = 8
				PlaySfx(15, 0)
#platform: Use_Haptics
				HapticEffect(50, 0, 0, 0)
#endplatform
			end if
		end if
		break

	case SIGNPOST_FLIP
		Music.Volume--
		Object.AnimationTimer++
		if Object.AnimationTimer == 2

			Object.AnimationTimer = 0
			Object.Frame++
			if Object.Frame == 5
				// Spawn a ring sparkle at random location close to the sign post
				Rand(TempValue0, 48)
				TempValue0 <<= 16
				TempValue0 += Object.XPos
				TempValue0 -= 1572864
				Rand(TempValue1, 32)
				TempValue1 <<= 16
				TempValue1 += Object.YPos
				TempValue1 -= 1835008
				CreateTempObject(TypeName[RingSparkle], 0, TempValue0, TempValue1)

				Object.Timer--
				if Object.Timer == 0

					if Object[0].Type == TypeName[BlankObject]	// Check if the player has already left (through a Special Ring)
						Object.State = SIGNPOST_BLANK
						if Object.PropertyValue < 3
							PlayMusic(1)
						else
							if Object.OutOfBounds == 0
								PlayMusic(1)
							end if
						end if
						Object[30].Type = TypeName[ActFinish]
						Object[30].DrawOrder = 6
						Object[30].Priority = 1
					else	// Otherwise the signpost will proceed as normal
						Player.ControlMode = -1
						Player.Up = 0
						Player.Down = 0
						Player.Left = 0
						Player.JumpPress = 0
						Player.JumpHold = 0
						Object.State = SIGNPOST_MOVE_PLAYER
					end if

				end if

			end if

			if Object.Frame == 9
				// Spawn a ring sparkle at random location close to the sign post
				Rand(TempValue0, 48)
				TempValue0 <<= 16
				TempValue0 += Object.XPos
				TempValue0 -= 1572864
				Rand(TempValue1, 32)
				TempValue1 <<= 16
				TempValue1 += Object.YPos
				TempValue1 -= 1835008
				CreateTempObject(TypeName[RingSparkle], 0, TempValue0, TempValue1)
				// Resets the animation cycle
				Object.Frame = 1
			end if

		end if
		// Freezes the player after leaving the screen
		TempValue0 = 40
		TempValue0 += Screen.CenterX
		TempValue0 <<= 16
		TempValue0 += Object.XPos
		if Player.XPos > TempValue0
			Player.XPos = TempValue0
			Player.Speed = 0
			Player.XVelocity = 0
		end if
		break

	case SIGNPOST_MOVE_PLAYER
		if Object[0].Type == TypeName[BlankObject]
			Object.State = SIGNPOST_BLANK
			if Object.PropertyValue < 3
				PlayMusic(1)
			else
				if Object.OutOfBounds == false
					PlayMusic(1)
				end if
			end if
			Object[30].Type = TypeName[ActFinish]
			Object[30].DrawOrder = 6
			Object[30].Priority = 1
		end if

		if Player.ControlMode == -1
			Player.Right = true
		end if

		TempValue0 = Screen.CenterX
		TempValue0 <<= 16
		TempValue0 += Object.XPos

		if Player.XPos > TempValue0
			if Object.PropertyValue < 3
				PlayMusic(1)
			else
				if Object.OutOfBounds == 0
					PlayMusic(1)
				end if
			end if
			Object.State = SIGNPOST_STOP_PLAYER
			Object[30].Type = TypeName[ActFinish]
			Object[30].DrawOrder = 6
			Object[30].Priority = 1
		end if
		break

	case SIGNPOST_STOP_PLAYER
		if Player.ControlMode == -1
			Player.Right = true
		end if
		TempValue0 = 40
		TempValue0 += Screen.CenterX
		TempValue0 <<= 16
		TempValue0 += Object.XPos
		if Player.XPos > TempValue0
			Player.XPos = TempValue0
			Player.Speed = 0
			Player.XVelocity = 0
		end if
		break

	case SIGNPOST_BLANK
		break
	end switch
end sub


sub ObjectDraw
	DrawSprite(0)
	DrawSprite(Object.Frame)
end sub


sub ObjectStartup
	if Stage.PlayerListPos == 0	// PlayerName[SONIC]
		LoadSpriteSheet("Global/Items2.gif")
	else
		LoadSpriteSheet("Global/Items2_t.gif")
	end if
	SpriteFrame(-12, -12, 24, 24, 0, 150)		// #0 - Sign Post
	SpriteFrame(-24, -44, 48, 32, 34, 99)		// #1 - Sign Post Head (Eggman)
	SpriteFrame(-16, -44, 32, 32, 1, 183)		// #2 - Sign Post Head (Flip 1)
	SpriteFrame(-4, -44, 8, 32, 25, 150)		// #3 - Sign Post Head (Flip 2)
	SpriteFrame(-16, -44, 32, 32, 1, 216)		// #4 - Sign Post Head (Flip 3)
	SpriteFrame(-24, -44, 48, 32, 34, 132)		// #5 - Sign Post Head (Player)
	SpriteFrame(-16, -44, 32, 32, 1, 183)		// #6 - Sign Post Head (Flip 1)
	SpriteFrame(-4, -44, 8, 32, 25, 150)		// #7 - Sign Post Head (Flip 2)
	SpriteFrame(-16, -44, 32, 32, 1, 216)		// #8 - Sign Post Head (Flip 3)
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[SignPost]
			Object[ArrayPos0].Priority = 3
			if Object[ArrayPos0].PropertyValue < 2
				TempValue0 = TileLayer[0].XSize
				TempValue0 -= 2
				TempValue1 = TileLayer[0].XSize
				TempValue2 = Object[ArrayPos0].iYPos
				TempValue2 += 64
				TempValue2 >>= 7
				GetTileLayerEntry(TempValue3, 0, TempValue0, TempValue2)
				SetTileLayerEntry(TempValue3, 0, TempValue1, TempValue2)
				TempValue0++
				TempValue1++
				GetTileLayerEntry(TempValue3, 0, TempValue0, TempValue2)
				SetTileLayerEntry(TempValue3, 0, TempValue1, TempValue2)
				if Object[ArrayPos0].PropertyValue == 1
					TempValue0--
					TempValue1--
					TempValue2--
					GetTileLayerEntry(TempValue3, 0, TempValue0, TempValue2)
					SetTileLayerEntry(TempValue3, 0, TempValue1, TempValue2)
					TempValue0++
					TempValue1++
					GetTileLayerEntry(TempValue3, 0, TempValue0, TempValue2)
					SetTileLayerEntry(TempValue3, 0, TempValue1, TempValue2)
				end if
			end if
		end if
		ArrayPos0++
	loop
end sub



sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.PropertyValue
			break
		case 0 // idk put the read stuff here
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			break
		case 0 // idk put the write stuff here
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-12, -12, 24, 24, 0, 150)
	SpriteFrame(-24, -44, 48, 32, 34, 99)

	// TODO: RSDKEdit stuff idk, I have no idea what prop val even does here
end sub

