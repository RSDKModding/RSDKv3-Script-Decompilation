//------------Sonic CD PauseMenu Script-------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.PropertyValue	:	Object.TimerThreshold
#alias Object.Value0		:	Object.Timer
#alias Object.Value1		:	Object.SelectedButton
#alias Object.Value2		:	Object.SpritesXPos
#alias Object.Value3		:	Object.BlackBarXPos
#alias Object.Value4		:	Object.BlackBarYPos
#alias Object.Value6		:	Object.BlockRestart

// States
#alias 0	:	PAUSEMENU_START_PAUSE
#alias 1	:	PAUSEMENU_SLIDE_IN
#alias 2	:	PAUSEMENU_SPRITES_FADE_IN
#alias 3	:	PAUSEMENU_CONTROLS
#alias 4	:	PAUSEMENU_CONFIRMED
#alias 5	:	PAUSEMENU_RESUME_GAME
#alias 6	:	PAUSEMENU_EXIT_LEVEL	// Not the best name but is essentially what will happen here

// Languages
#alias 0	:	ENGLISH
#alias 1	:	FRENCH
#alias 2	:	ITALIAN
#alias 3	:	DEUTSCH
#alias 4	:	SPANISH
#alias 5	:	JAPANESE

// Engine & Callbacks
#alias 2	:	MESSAGE_LOSTFOCUS
#alias 3	:	MESSAGE_MESSAGE_3
#alias 4	:	MESSAGE_MESSAGE_4

#alias 6	:	CALLBACK_RESTART_SELECTED
#alias 7	:	CALLBACK_EXIT_SELECTED
#alias 8	:	CALLBACK_BUY_FULL_GAME_SELECTED

sub ObjectMain
	switch Object.State
	case PAUSEMENU_START_PAUSE
		PauseMusic()
		Object.XPos = Screen.XSize
		Object.BlackBarXPos = 0
		Object.Timer = 0
		Object.InkEffect = 2 //INK_ALPHA
		Object.Alpha = 0
		switch Engine.Language
		case ENGLISH
		case JAPANESE
			Object.TimerThreshold = 12
			break

		case FRENCH
			Object.TimerThreshold = 15
			break

		case ITALIAN
		case DEUTSCH
		case SPANISH
			Object.TimerThreshold = 13
			break

		end switch

		if Player.Lives < 2
			Object.BlockRestart = true
		else
			if Stage.ActiveList == SPECIAL_STAGE
				Object.BlockRestart = true
			else
				Object.BlockRestart = false
			end if
		end if
#platform: Use_Haptics			
		HapticEffect(-1, 0, 0, 0)
#endplatform

#platform: Mobile
		if Options.TouchControls == true
			Object.SelectedButton = -1
			Options.PhysicalControls = false
		else
			Options.PhysicalControls = true
		end if
#endplatform
		Object.State++
		break

	case PAUSEMENU_SLIDE_IN
		Object.BlackBarXPos = Object.Timer
		Object.BlackBarXPos *= Screen.XSize
		Object.BlackBarXPos /= 12

		Object.BlackBarYPos = Object.Timer
		Object.BlackBarYPos *= 240
		Object.BlackBarYPos /= 12
		
		TempValue0 = Object.Timer
		TempValue0 <<= 7
		TempValue0 /= 12
		Object.XPos = Screen.XSize
		Object.XPos -= TempValue0

		if Object.Timer < Object.TimerThreshold
			Object.Timer++
		else
			PauseMusic()
			Object.SpritesXPos = Object.XPos
			Object.SpritesXPos += 48
			Object.Timer = 0
			Object.State++
		end if
		break

	case PAUSEMENU_SPRITES_FADE_IN
		if Object.Timer < 256
			Object.Timer += 16
			if Object.Timer < 255
				Object.Alpha = Object.Timer
			else
				Object.Alpha = 255
			end if
		else
			Object.Timer = 0
			Object.Alpha = 255
			Object.State++
		end if
		break

	case PAUSEMENU_CONTROLS
#platform: Mobile
		CheckEqual(Options.PhysicalControls,true)
#endplatform
#platform: Standard
		CheckResult = true
#endplatform
		if CheckResult == true
			if KeyPress.Up == true
				PlaySfx(23, 0) 	//Menu Button SFX
				Object.Timer = 0
				Object.AnimationTimer = 0
				Object.Frame = 0
				Object.SpritesXPos = Screen.XSize
				Object.SelectedButton--
				if Options.DevMenuFlag == true
					if Object.SelectedButton < 0	 
						Object.SelectedButton = 3
					end if
				else
					if Object.SelectedButton < 0
						Object.SelectedButton = 2
					end if
				end if
				if Object.BlockRestart == true
					if Object.SelectedButton == 1
						Object.SelectedButton--
					end if
				end if
			end if

			if KeyPress.Down == true
				PlaySfx(23, 0) 	//Menu Button SFX
				Object.Timer = 0
				Object.AnimationTimer = 0
				Object.Frame = 0
				Object.SpritesXPos = Screen.XSize
				Object.SelectedButton++
				if Options.DevMenuFlag == true
					if Object.SelectedButton > 3
						Object.SelectedButton = 0
					end if
				else
					if Object.SelectedButton > 2
						Object.SelectedButton = 0
					end if
				end if
				if Object.BlockRestart == true
					if Object.SelectedButton == 1
						Object.SelectedButton++
					end if
				end if
			end if

			if KeyPress.Start == true
				PlaySfx(27, 0)	//Select SFX
				Object.State = PAUSEMENU_CONFIRMED
				Object.Alpha = 248
				Object.Timer = 0
			end if

			if KeyPress.ButtonA == true
				PlaySfx(27, 0)	//Select SFX
				Object.State = PAUSEMENU_CONFIRMED
				Object.Alpha = 248
				Object.Timer = 0
			end if
		end if

#platform: Mobile
		if Options.PhysicalControls == true
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			if CheckResult > -1
				Options.PhysicalControls = false
				Object.SelectedButton = -1
			end if
		else
			if Engine.Message == MESSAGE_LOSTFOCUS
				Object.SelectedButton = -1
			end if
			CheckTouchRect(0, 0, Screen.XSize, Screen.YSize)
			TempValue3 = CheckResult
			CheckTouchRect(TempValue0, 32, Screen.XSize, 64)
			if CheckResult > -1
				Object.SelectedButton = 0
			else
				if TempValue3 < 0
					if Object.SelectedButton == 0
						PlaySfx(27, 0)	//Select SFX
						Object.State = PAUSEMENU_CONFIRMED
						Object.Alpha = 248
						Object.Timer = 0
					end if
				else
					if Object.SelectedButton == 0
						Object.SelectedButton = -1
					end if
				end if
			end if

			if Object.BlockRestart == false
				CheckTouchRect(TempValue0, 80, Screen.XSize, 112)
				if CheckResult > -1
					Object.SelectedButton = 1
				else
					if TempValue3 < 0
						if Object.SelectedButton == 1
							PlaySfx(27, 0)	//Select SFX
							Object.State = PAUSEMENU_CONFIRMED
							Object.Alpha = 248
							Object.Timer = 0
						end if
					else
						if Object.SelectedButton == 1
							Object.SelectedButton = -1
						end if
					end if
				end if
			end if

			CheckTouchRect(TempValue0, 128, Screen.XSize, 160)
			if CheckResult > -1
				Object.SelectedButton = 2
			else
				if TempValue3 < 0
					if Object.SelectedButton == 2
						PlaySfx(27, 0)	//Select SFX
						Object.State = PAUSEMENU_CONFIRMED
						Object.Alpha = 248
						Object.Timer = 0
					end if
				else
					if Object.SelectedButton == 2
						Object.SelectedButton = -1
					end if
				end if
			end if

			if Options.DevMenuFlag == true
				CheckTouchRect(TempValue0, 176, Screen.XSize, 208)
				if CheckResult > -1
					Object.SelectedButton = 3
				else
					if TempValue3 < 0
						if Object.SelectedButton == 3
							PlaySfx(27, 0)	//Select SFX
							Object.State = PAUSEMENU_CONFIRMED
							Object.Alpha = 248
							Object.Timer = 0
						end if
					else
						if Object.SelectedButton == 3
							Object.SelectedButton = -1
						end if
					end if
				end if
			end if

			if Engine.TrialMode == true
				CheckTouchRect(TempValue0, 176, Screen.XSize, 208)
				if CheckResult > -1
					Object.SelectedButton = 3
				else
					if TempValue3 < 0
						if Object.SelectedButton == 3
							PlaySfx(27, 0)	//Select SFX
							Object.State = PAUSEMENU_CONFIRMED
							Object.Alpha = 248
							Object.Timer = 0
						end if
					else
						if Object.SelectedButton == 3
							Object.SelectedButton = -1
						end if
					end if
				end if
			end if

			if KeyPress.Up == true
				if Options.DevMenuFlag == true
					Object.SelectedButton = 3
				else
					Object.SelectedButton = 2
				end if
				Options.PhysicalControls = true
			end if

			if KeyPress.Down == true
				Object.SelectedButton = 0
				Options.PhysicalControls = true
			end if
		end if
#endplatform

		if Object.Timer < 60
			Object.Timer++
		else
			Object.Frame = Object.AnimationTimer
			Object.Frame >>= 4
			Object.Frame++
			Object.AnimationTimer++
			Object.AnimationTimer &= 31
		end if
		break

	case PAUSEMENU_CONFIRMED
		Object.Timer++
		Object.Timer &= 3

		Object.SpritesXPos += 4
		if Object.Alpha > 0
			Object.Alpha -= 8
		else
			if Object.SelectedButton == 0
				Object.SpritesXPos = Object.XPos
				Object.SpritesXPos += 48
				Object.BlackBarYPos = 202
				Object.Timer = 0
				Object.Alpha = 128
				Object.State = PAUSEMENU_RESUME_GAME
				Stage.State = STAGE_RUNNING
			else
#platform: Standard
				Object.SpritesXPos = Object.XPos
				Object.SpritesXPos += 48
				StopMusic()
				Object.State = PAUSEMENU_EXIT_LEVEL
#endplatform
#platform: Mobile
				if Engine.Message < MESSAGE_MESSAGE_3
					switch Object.SelectedButton
					case 1
						EngineCallback(CALLBACK_RESTART_SELECTED)
						break
					case 2
						EngineCallback(CALLBACK_EXIT_SELECTED)
						break
					case 3
						if Engine.TrialMode == false
							Object.SpritesXPos = Object.XPos
							Object.SpritesXPos += 48
							StopMusic()
							Object.State = PAUSEMENU_EXIT_LEVEL	// Dev Menu
						else
							EngineCallback(CALLBACK_BUY_FULL_GAME_SELECTED)	// Demo End
							Object.Timer = 0
							Object.AnimationTimer = 0
							Object.Frame = 0
							Object.SpritesXPos = Screen.XSize
							if Options.PhysicalControls == false
								Object.SelectedButton = -1
							end if
							Object.State = PAUSEMENU_CONTROLS
							Object.Alpha = 255
						end if
						break
					end switch
				else
					if Engine.Message == MESSAGE_MESSAGE_4
						Object.Timer = 0
						Object.AnimationTimer = 0
						Object.Frame = 0
						Object.SpritesXPos = Screen.XSize
						if Options.PhysicalControls == false
							Object.SelectedButton = -1
						end if
						Object.State = PAUSEMENU_CONTROLS
						Object.Alpha = 255
					else
						Object.SpritesXPos = Object.XPos
						Object.SpritesXPos += 48
						StopMusic()
						Object.State = PAUSEMENU_EXIT_LEVEL
					end if
				end if
#endplatform
			end if
		end if
		break

	case PAUSEMENU_RESUME_GAME
		if Object.Alpha > 0
			Object.Alpha -= 8
			Object.XPos += 16
			Object.SpritesXPos += 16
			Object.BlackBarYPos += 16
		else
			ResumeMusic()
			ResetObjectEntity(Object.EntityNo, TypeName[BlankObject], 0, 0, 0)
			if Player.Animation == ANI_FLYING
				PlaySfx(24, 1)
			end if
			if Player.Animation == ANI_FLYINGTIRED
				PlaySfx(24, 1)
			end if
			if Engine.FrameSkipSetting > 0
				Engine.FrameSkipTimer = 0
			end if
		end if
		break

	case PAUSEMENU_EXIT_LEVEL
#platform: Standard
		CheckGreater(Object.XPos, -32)
#endplatform
#platform: Mobile
		CheckGreater(Object.XPos, -64)
#endplatform

		if CheckResult == true
			Object.XPos -= 16
			Object.SpritesXPos += 16
		else
			switch Object.SelectedButton
			case 1		// Restart
				LampPost.Check = 0
				if Options.GameMode == 2
					Player.Lives = 1
					Player.Score = 0
				else
					Player.Lives--
				end if
				LoadStage()
				break
			case 2		// Exit
				Good_Future_Count = 0
				Good_Future_List = 0
				Good_Future = 0
				Transporter_Destroyed = 0
				MetalSonic_Destroyed = 0
				Stage.ActiveList = PRESENTATION_STAGE
				LampPost.Check = 0
				if Options.GameMode == 2
					Stage.ListPos = 2	// Time Attack
					TimeAttack.Result = 0
				else
					Stage.ListPos = 0	// Title Screen
				end if
				LoadStage()
				break
			case 3		// Dev Menu
				TimeAttack.Round = -1
				Engine.State = RESET_GAME
				break
			end switch
		end if
		break

	end switch
end sub


sub ObjectDraw
#platform: Standard
	SetActivePalette(0, 0, Screen.YSize)
#endplatform

	switch Object.State
	case PAUSEMENU_START_PAUSE
	case PAUSEMENU_SLIDE_IN
	case PAUSEMENU_SPRITES_FADE_IN
		// Pause White BG
		if Object.Value5 < 240
			if Object.BlackBarYPos > Object.Value5
				TempValue0 = Object.BlackBarYPos
				TempValue0 -= Object.Value5
				DrawRect(0, Object.Value5, 384, TempValue0, 255, 255, 255, 128)
				Object.Value5 = Object.BlackBarYPos
			end if
		end if
		break

	case PAUSEMENU_RESUME_GAME
		// White BG
		if Object.Alpha < 128
			DrawRect(0, 0, Screen.XSize, 240, 255, 255, 255, Object.Alpha)
		end if
		break

	case PAUSEMENU_EXIT_LEVEL
		// Screen Cover
		TempValue0 = Screen.XSize
		TempValue0 -= 96
		DrawRect(TempValue0, 0, 96, 240, 0, 0, 0, 255)
		break
		endswitch
	
	// Menu Draw
	DrawSpriteScreenXY(3, Object.XPos, 0)
	DrawSpriteScreenXY(3, Object.XPos, 32)
	DrawSpriteScreenXY(3, Object.XPos, 64)
	DrawSpriteScreenXY(3, Object.XPos, 96)
	DrawSpriteScreenXY(3, Object.XPos, 128)
	DrawSpriteScreenXY(3, Object.XPos, 160)
	DrawSpriteScreenXY(3, Object.XPos, 192)
	DrawSpriteScreenXY(3, Object.XPos, 224)

	// Pause Black Bar
	TempValue0 = Object.XPos
	TempValue0 += 128
	DrawRect(TempValue0, 0, 128, Screen.YSize, 0, 0, 0, 255)

	switch Object.State
	case PAUSEMENU_START_PAUSE
	case PAUSEMENU_SLIDE_IN
	case PAUSEMENU_SPRITES_FADE_IN
	case PAUSEMENU_CONTROLS
	case PAUSEMENU_CONFIRMED
		DrawRect(0, 202, Object.BlackBarXPos, 13, 0, 0, 0, 255)
		if Object.State < 4
			DrawSpriteScreenFX(4, FX_INK, 0, 202)
		else
			DrawSpriteScreenXY(4, 0, 202)
		end if
		break
		
	case PAUSEMENU_RESUME_GAME
		DrawRect(0, Object.BlackBarYPos, Screen.XSize, 13, 0, 0, 0, 255)
		DrawSpriteScreenXY(4, 0, Object.BlackBarYPos)
		break
		endswitch
	
	// Text Sprite Slide
	if Object.State < 5
		TempValue0 = Object.XPos
		TempValue0 += 48
	end if
	
	switch Object.State
	case PAUSEMENU_START_PAUSE
	case PAUSEMENU_SLIDE_IN
	case PAUSEMENU_SPRITES_FADE_IN
	case PAUSEMENU_CONTROLS

		if Object.SpritesXPos > TempValue0
			TempValue1 = TempValue0
			TempValue1 -= Object.SpritesXPos
			TempValue1 >>= 3
			Object.SpritesXPos += TempValue1
		end if
		
		// Continue
		TempValue1 = 48
		DrawSpriteScreenFX(6, FX_INK, TempValue0, TempValue1)

		// Continue Highlight
		if Object.SelectedButton == 0
			DrawSpriteScreenFX(5, FX_INK, Object.SpritesXPos, TempValue1)
			DrawSpriteScreenFX(Object.Frame, FX_INK, TempValue0, TempValue1)
		end if

		// Restart
		TempValue1 += 48
		if Object.BlockRestart == 0
			DrawSpriteScreenFX(7, FX_INK, TempValue0, TempValue1)
		else
#platform: Standard	   
			DrawSpriteScreenFX(10, FX_INK, TempValue0, TempValue1)
#endplatform
#platform: Mobile
			TempValue2 = Object.Alpha
			Object.Alpha >>= 1
			DrawSpriteScreenFX(7, FX_INK, TempValue0, TempValue1)
			Object.Alpha = TempValue2
#endplatform	  
		end if

		// Restart Highlight
		if Object.SelectedButton == 1
			DrawSpriteScreenFX(5, FX_INK, Object.SpritesXPos, TempValue1)
			DrawSpriteScreenFX(Object.Frame, FX_INK, TempValue0, TempValue1)
		end if
		
		// Exit
		TempValue1 += 48
		DrawSpriteScreenFX(8, FX_INK, TempValue0, TempValue1)

		// Exit Highlight
		if Object.SelectedButton == 2
			DrawSpriteScreenFX(5, FX_INK, Object.SpritesXPos, TempValue1)
			DrawSpriteScreenFX(Object.Frame, FX_INK, TempValue0, TempValue1)
		end if

		// Dev Menu
		TempValue1 += 48
		if Options.DevMenuFlag == true
			DrawSpriteScreenFX(9, FX_INK, TempValue0, TempValue1)
			if Object.SelectedButton == 3
				DrawSpriteScreenFX(5, FX_INK, Object.SpritesXPos, TempValue1)
				DrawSpriteScreenFX(Object.Frame, FX_INK, TempValue0, TempValue1)
			end if
		end if
		
#platform: Mobile	// Demo Restart
		if Options.DevMenuFlag == false
			if Engine.TrialMode == true
				DrawSpriteScreenFX(10, FX_INK, TempValue0, TempValue1)
				if Object.SelectedButton == 3
					DrawSpriteScreenFX(5, FX_INK, Object.SpritesXPos, TempValue1)
					DrawSpriteScreenFX(Object.Frame, FX_INK, TempValue0, TempValue1)
				end if
			end if
		end if
#endplatform
		break

	case PAUSEMENU_CONFIRMED

		//	Continue Flashing
		TempValue1 = 48
		if Object.SelectedButton == 0
			if Object.Timer < 2
				DrawSpriteScreenXY(6, TempValue0, TempValue1)
			end if
			DrawSpriteScreenXY(5, Object.SpritesXPos, TempValue1)
			DrawSpriteScreenXY(Object.Frame, TempValue0, TempValue1)
		else
			DrawSpriteScreenFX(6, FX_INK, TempValue0, TempValue1)
		end if

		//	Restart Flashing
		TempValue1 += 48
		if Object.SelectedButton == 1
			if Object.Timer < 2
				DrawSpriteScreenXY(7, TempValue0, TempValue1)
			end if
			DrawSpriteScreenXY(5, Object.SpritesXPos, TempValue1)
			DrawSpriteScreenXY(Object.Frame, TempValue0, TempValue1)
		else
			if Object.BlockRestart == 0
				DrawSpriteScreenFX(7, FX_INK, TempValue0, TempValue1)
			else
#platform: Standard
				DrawSpriteScreenFX(10, FX_INK, TempValue0, TempValue1)
#endplatform	
#platform: Mobile
				TempValue2 = Object.Alpha
				Object.Alpha >>= 1
				DrawSpriteScreenFX(7, FX_INK, TempValue0, TempValue1)
				Object.Alpha = TempValue2
#endplatform	   
			end if
		end if

		//	Exit Flashing
		TempValue1 += 48
		if Object.SelectedButton == 2
			if Object.Timer < 2
				DrawSpriteScreenXY(8, TempValue0, TempValue1)
			end if
			DrawSpriteScreenXY(5, Object.SpritesXPos, TempValue1)
			DrawSpriteScreenXY(Object.Frame, TempValue0, TempValue1)
		else
			DrawSpriteScreenFX(8, FX_INK, TempValue0, TempValue1)
		end if

		//	Dev Menu Flashing
		TempValue1 += 48
		if Options.DevMenuFlag == true
			if Object.SelectedButton == 3
				if Object.Timer < 2
					DrawSpriteScreenXY(9, TempValue0, TempValue1)
				end if
				DrawSpriteScreenXY(5, Object.SpritesXPos, TempValue1)
				DrawSpriteScreenXY(Object.Frame, TempValue0, TempValue1)
			else
				DrawSpriteScreenFX(9, FX_INK, TempValue0, TempValue1)
			end if
		end if

#platform: Mobile	// Restart Demo Flashing
		if Options.DevMenuFlag == false
			if Engine.TrialMode == true
				if Object.SelectedButton == 3
					if Object.Timer < 2
						DrawSpriteScreenXY(10, TempValue0, TempValue1)
					end if
					DrawSpriteScreenXY(5, Object.SpritesXPos, TempValue1)
					DrawSpriteScreenXY(Object.Frame, TempValue0, TempValue1)
				else
					DrawSpriteScreenFX(10, FX_INK, TempValue0, TempValue1)
				end if
			end if
		end if
#endplatform
		break

	case PAUSEMENU_RESUME_GAME
	case PAUSEMENU_EXIT_LEVEL

		switch Object.SelectedButton
		case 0	// Continue
			DrawSpriteScreenXY(6, Object.SpritesXPos, 48)
			DrawSpriteScreenXY(Object.Frame, Object.SpritesXPos, 48)
			break

		case 1	// Restart
			DrawSpriteScreenXY(7, Object.SpritesXPos, 96)
			DrawSpriteScreenXY(Object.Frame, Object.SpritesXPos, 96)
			break

		case 2	// Exit
			DrawSpriteScreenXY(8, Object.SpritesXPos, 144)
			DrawSpriteScreenXY(Object.Frame, Object.SpritesXPos, 144)
			break

		case 3	// Dev Menu
			if Options.DevMenuFlag == true
				DrawSpriteScreenXY(9, Object.SpritesXPos, 192)
				DrawSpriteScreenXY(Object.Frame, Object.SpritesXPos, 192)
			end if

#platform: Mobile	// Demo Restart
			if Options.DevMenuFlag == false
				if Engine.TrialMode == true
					DrawSpriteScreenXY(10, Object.SpritesXPos, 192)
					DrawSpriteScreenXY(Object.Frame, Object.SpritesXPos, 192)
				end if
			end if
#endplatform
			break
		end switch
		break

	end switch
end sub


sub ObjectStartup
	switch Engine.Language
	case ENGLISH
	case JAPANESE
		LoadSpriteSheet("Global/Pause_EN.gif")
		break
	case FRENCH
		LoadSpriteSheet("Global/Pause_FR.gif")
		break
	case ITALIAN
		LoadSpriteSheet("Global/Pause_IT.gif")
		break
	case DEUTSCH
		LoadSpriteSheet("Global/Pause_DE.gif")
		break
	case SPANISH
		LoadSpriteSheet("Global/Pause_ES.gif")
		break
	end switch
	if Stage.PlayerListPos == 0
		SpriteFrame(-28, -14, 16, 24, 1, 103)	// #0  - Sonic
		SpriteFrame(-28, -14, 16, 24, 18, 103)	// #1  - Sonic Waiting Frame 0
		SpriteFrame(-28, -14, 16, 24, 35, 103)	// #2  - Sonic Waiting Frame 1
	else
		SpriteFrame(-28, -14, 16, 24, 52, 103)	// #0  - Tails
		SpriteFrame(-28, -14, 16, 24, 69, 103)	// #1  - Tails Waiting Frame 0
		SpriteFrame(-28, -14, 16, 24, 86, 103)	// #2  - Tails Waiting Frame 1
	end if
	
	SpriteFrame(0, 0, 128, 32, 0, 0)			// #3  - Menu BG Section
	SpriteFrame(0, -19, 128, 32, 0, 32)			// #4  - Pause
	SpriteFrame(-6, 7, 128, 3, 0, 65)			// #5  - White Bar
	switch Engine.Language
	case ENGLISH
	case JAPANESE
		SpriteFrame(0, -5, 65, 11, 0, 69)		// #6  - Continue
		SpriteFrame(0, -5, 55, 11, 0, 81)		// #7  - Restart
		SpriteFrame(0, -5, 30, 11, 66, 69)		// #8  - Exit
		SpriteFrame(0, -5, 64, 11, 56, 81)		// #9  - Dev Menu
#platform: Standard
		SpriteFrame(0, -5, 55, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
#platform: Mobile
		SpriteFrame(0, -5, 72, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
		break

	case FRENCH
		SpriteFrame(0, -5, 73, 11, 0, 69)		// #6  - Continue
		SpriteFrame(0, -5, 95, 11, 0, 81)		// #7  - Restart
		SpriteFrame(0, -5, 53, 11, 74, 69)		// #8  - Exit
		SpriteFrame(0, -5, 23, 11, 96, 81)		// #9  - Dev Menu
#platform: Standard
		SpriteFrame(0, -5, 95, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
#platform: Mobile
		SpriteFrame(0, -5, 72, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
		break
		
	case ITALIAN
		SpriteFrame(0, -5, 65, 11, 0, 69)		// #6  - Continue
		SpriteFrame(0, -5, 78, 11, 0, 81)		// #7  - Restart
		SpriteFrame(0, -5, 28, 11, 66, 69)		// #8  - Exit
		SpriteFrame(0, -5, 23, 11, 79, 81)		// #9  - Dev Menu
#platform: Standard
		SpriteFrame(0, -5, 78, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
#platform: Mobile
		SpriteFrame(0, -5, 72, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
		break

	case DEUTSCH
		SpriteFrame(0, -5, 48, 11, 0, 69)		// #6  - Continue
		SpriteFrame(0, -5, 65, 11, 0, 81)		// #7  - Restart
		SpriteFrame(0, -5, 71, 11, 49, 69)		// #8  - Exit
		SpriteFrame(0, -5, 23, 11, 66, 81)		// #9  - Dev Menu
#platform: Standard
		SpriteFrame(0, -5, 65, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
#platform: Mobile
		SpriteFrame(0, -5, 72, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
		break
		
	case SPANISH
		SpriteFrame(0, -5, 73, 11, 0, 69)		// #6  - Continue
		SpriteFrame(0, -5, 67, 11, 0, 81)		// #7  - Restart
		SpriteFrame(0, -5, 36, 11, 74, 69)		// #8  - Exit
		SpriteFrame(0, -5, 23, 11, 68, 81)		// #9  - Dev Menu
#platform: Standard
		SpriteFrame(0, -5, 72, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
#platform: Mobile
		SpriteFrame(0, -5, 55, 11, 0, 92)		// #10 - Blocked Restart
#endplatform
		break

	end switch
end sub


sub RSDKEdit
	if Editor.ReturnVariable == true
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			CheckResult = Object.PropertyValue
			break
		case X // idk put your read stuff here
			CheckResult = Object.PropertyValue
			break
		end switch
	else
		switch Editor.VariableID
		case EDIT_VAR_PROPVAL // property value
			Object.PropertyValue = Editor.VariableValue
			break
		case X // idk put your write stuff here
			Object.PropertyValue = Editor.VariableValue
			break
		end switch
	end if
end sub


sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)

	// Although used by the object, it shouldn't be set here
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub

