//----------------Sonic CD Metal Sonic Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value1		:	Object.YVelocity
#alias Object.Value3		:	Object.XVelocity
#alias Object.Value4		:	Object.FrameTimer

// Amy Aliases
#alias Object[-1].XPos		:	Amy.XPos
#alias Object[-1].YPos		:	Amy.YPos
#alias Object[-1].Direction	:	Amy.Direction
#alias Object[-1].Frame		:	Amy.Frame
#alias Object[-1].State		:	Amy.State

// States
#alias 0	:	METALSONIC_WAIT
#alias 1	:	METALSONIC_APPROACH
#alias 2	:	METALSONIC_CATCH_AMY
#alias 3	:	METALSONIC_INCREASE_HEIGHT
#alias 4	:	METALSONIC_STARE
#alias 5	:	METALSONIC_FLEE

// Amy States
#alias 4	:	AMY_KIDNAP


sub ObjectMain
	switch Object.State
	case METALSONIC_WAIT	// Used by Amy
		break

	case METALSONIC_APPROACH
		Object.XPos -= 0x30000
		Object.Frame++
		Object.Frame &= 1

		TempValue0 = 0

		ArrayPos0 = Object.EntityNo
		ArrayPos0++
		while TempValue0 < 3
			if Object[ArrayPos0].Type > TypeName[Blank Object]
				TempValue1  = Object[ArrayPos0].XPos
				TempValue1 += 0x240000

				if Object.XPos < TempValue1
					// Destroy Spikes
					Object[ArrayPos0].Type = TypeName[Blank Object]
					TempValue2   = Object[ArrayPos0].PropertyValue
					TempValue2 <<= 2

					// Create Debris
					CreateTempObject(TypeName[SpikeDebris], TempValue2, Object[ArrayPos0].XPos, Object[ArrayPos0].YPos)
					Object[TempObjectPos].iXPos -= 9
					Object[TempObjectPos].iYPos -= 9
					Object[TempObjectPos].XVelocity = -0x10000
					Object[TempObjectPos].YVelocity = -0x30000
					TempValue2++

					CreateTempObject(TypeName[SpikeDebris], TempValue2, Object[ArrayPos0].XPos, Object[ArrayPos0].YPos)
					Object[TempObjectPos].iXPos += 8
					Object[TempObjectPos].iYPos -= 9
					Object[TempObjectPos].XVelocity =  0x10000
					Object[TempObjectPos].YVelocity = -0x30000
					TempValue2++

					CreateTempObject(TypeName[SpikeDebris], TempValue2, Object[ArrayPos0].XPos, Object[ArrayPos0].YPos)
					Object[TempObjectPos].iXPos -= 9
					Object[TempObjectPos].iYPos += 8
					Object[TempObjectPos].XVelocity = -0x10000
					Object[TempObjectPos].YVelocity = -0x18000
					TempValue2++

					CreateTempObject(TypeName[SpikeDebris], TempValue2, Object[ArrayPos0].XPos, Object[ArrayPos0].YPos)
					Object[TempObjectPos].iXPos += 8
					Object[TempObjectPos].iYPos += 8
					Object[TempObjectPos].XVelocity =  0x10000
					Object[TempObjectPos].YVelocity = -0x18000
					TempValue2++

					PlayStageSfx(8, 0)

#platform: Use_Haptics
					HapticEffect(96, 0, 0, 0)
#endplatform
				end if
			end if
			ArrayPos0++
			TempValue0++
		loop
		TempValue0  = Amy.XPos
		TempValue0 += 0x200000
		if Object.XPos < TempValue0
			Object.State = METALSONIC_CATCH_AMY
			Object.Frame = 2
			Object.YVelocity = Object.YPos
			Amy.Frame = 8
			Amy.State = AMY_KIDNAP
			PlayStageSfx(9, 0)
		end if
		break

	case METALSONIC_CATCH_AMY
		if Object.Timer > 8

			if Player.XPos < Object.XPos
				Object.Direction = FACING_LEFT
			else
				Object.Direction = FACING_RIGHT
			end if

		end if

		Sin(Object.YPos, Object.Timer)
		Object.YPos <<= 9
		Object.YPos  += Object.YVelocity

		Object.Timer += 8
		if Object.Timer > 607
			Object.State = METALSONIC_INCREASE_HEIGHT
			Object.Timer = 0
		end if
		break

	case METALSONIC_INCREASE_HEIGHT

		if Player.XPos < Object.XPos
			Object.Direction = FACING_LEFT
		else
			Object.Direction = FACING_RIGHT
		end if

		if Object.Timer < 40
			Object.Timer++
			Object.YVelocity -= 0x20000
			Object.YPos -= 0x20000
		else
			Object.State = METALSONIC_STARE
			Object.Timer = 256
		end if
		break

	case METALSONIC_STARE

		if Player.XPos < Object.XPos
			Object.Direction = FACING_LEFT
		else
			Object.Direction = FACING_RIGHT
		end if

		Sin(Object.YPos, Object.Timer)
		Object.YPos <<= 9
		Object.YPos  += Object.YVelocity

		Object.Timer += 8
		if Object.Timer > 976
			Object.State = METALSONIC_FLEE
			Object.Direction = FACING_RIGHT
		end if
		break

	case METALSONIC_FLEE

		if Object.XVelocity < 0x60000
			Object.XVelocity += 0x2000
		end if
		Object.XPos += Object.XVelocity

		if Object.OutOfBounds == true
			// Erase Metal
			TempValue0 = Object.EntityNo
			ResetObjectEntity(TempValue0, TypeName[Blank Object], 0, 0, 0)
			// Erase Amy
			TempValue0--
			ResetObjectEntity(TempValue0, TypeName[Blank Object], 0, 0, 0)
		end if
		break
	end switch
end sub


sub ObjectDraw
	if Object.State < METALSONIC_CATCH_AMY
		DrawSprite(Object.Frame)
	else
		Amy.XPos = Object.XPos
		Amy.YPos = Object.YPos
		Amy.Direction = Object.Direction

		TempValue0   = Object.FrameTimer
		TempValue0 >>= 1
		TempValue0  += 3
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)

		Object.FrameTimer++
		Object.FrameTimer &= 7
		DrawSpriteFX(Object.Frame, FX_FLIP, Object.XPos, Object.YPos)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R3/Objects.gif")

	// Overdrive Attack
	SpriteFrame(-24, -16, 48, 32, 67, 68)	// #0 - Overdrive Attack Frame 0
	SpriteFrame(-24, -16, 48, 32, 116, 68)	// #1 - Overdrive Attack Frame 1

	// Metal Idle
	SpriteFrame(-16, -24, 32, 48, 1, 108)	// #2 - Metal Sonic

	// Booster
	SpriteFrame(-24, -12, 24, 24, 214, 43)	// #3 - Booster Frame 0
	SpriteFrame(0, 0, 2, 2, 165, 68)		// #4 - Booster Frame 1 / Cheat Sprite
	SpriteFrame(-28, -16, 32, 32, 165, 68)	// #5 - Booster Frame 2
	SpriteFrame(0, 0, 2, 2, 165, 68)		// #6 - Booster Frame 3 / Cheat Sprite
end sub

// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub

sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
