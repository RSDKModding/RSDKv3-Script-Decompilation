//----------------Sonic CD Boss Face 2 Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//
//---------Used on Mission "M099 - Eggman: Speed Up"----------//

// Aliases
#alias Object.Value0		:	Object.Oscillation
#alias Object.Value1		:	Object.BodyEntity
#alias Object.Value2		:	Object.ShoulderEntity
#alias Object.Value3		:	Object.Hit
#alias Object.Value4		:	Object.BitHolder
#alias Object.Value5		:	Object.NewXBoundary1Value
#alias Object.Value6		:	Object.NewXBoundary2Value
#alias Object.Value7		:	Object.SavedXBoundary2

#alias Object.Rotation		:	Object.Timer
#alias Object.Direction		:	Object.FaceTimer
#alias Object.Scale			:	Object.SavedYBoundary2
#alias Object.InkEffect		:	Object.FrameTimer
#alias Object.PropertyValue	:	Object.BossSpeed

// EGG-HVC-001 Body Aliases
#alias Object[+1].iXPos		:	BossBody.iXPos
#alias Object[+1].iYPos		:	BossBody.iYPos
#alias Object[+1].YPos		:	BossBody.YPos
#alias Object[+1].State		:	BossBody.State
#alias Object.Value0		:	Body.FaceEntity
#alias Object.Value1		:	Body.LegJointREntity
#alias Object.Value2		:	Body.LegJointLEntity

// Value4 is used to check key events, forward/backward movement and currently active leg
#alias Object.Value4		:	Body.BitHolder
#alias Object.Value5		:	Body.ChargeStart
#alias Object.State			:	Body.State

// General Aliases
#alias Object.Priority		:	BossPart.Priority
#alias Object.DrawOrder		:	BossPart.DrawOrder
#alias Object.Value0		:	BossPart.PrevEntity
#alias Object.Value1		:	BossPart.NextEntity

// HVC-001 Boss Right Leg Joint Aliases
#alias Object[+2].YPos		:	BossLegJointR.YPos

// HVC-001 Boss Right Leg Aliases
#alias Object[+3].YPos		:	BossLegR.YPos
#alias Object[+3].Value7	:	BossLegR.YVelocity

// HVC-001 Boss Right Foot Aliases
#alias Object[+4].Value7	:	BossFootR.YVelocity
// HVC-001 Boss Left Leg Joint Aliases
#alias Object[+5].YPos		:	BossLegJointL.YPos

// HVC-001 Boss Left Leg Aliases
#alias Object[+6].Value7	:	BossLegL.YVelocity

// HVC-001 Boss Left Foot Aliases
#alias Object[+7].Value7	:	BossFootL.YVelocity

// HVC-001 Boss Shoulder Aliases
#alias Object[+8].YPos		:	BossShoulder.YPos
#alias Object.Value0		:	Shoulder.FaceEntity
#alias Object.Value1		:	Shoulder.ArmJointLEntity
#alias Object.Value2		:	Shoulder.ArmJointREntity

// HVC-001 Boss Left Arm Joint Aliases
#alias Object[+9].YPos		:	BossArmJointL.YPos

// HVC-001 Boss Left Arm Aliases
#alias Object[+10].YPos		:	BossArmL.YPos
// HVC-001 Boss Left Bumper Joint Aliases
#alias Object[+11].YPos		:	BossBumperL.YPos

// HVC-001 Boss Right Arm Joint Aliases
#alias Object[+12].YPos		:	BossArmJointR.YPos

// HVC-001 Boss Right Arm Aliases
#alias Object[+13].YPos		:	BossArmR.YPos

// HVC-001 Boss Right Bumper Joint Aliases
#alias Object[+14].YPos		:	BossBumperR.YPos

// Debris Aliases
#alias Object.State			:	Debris.State
#alias Object.Value2		:	Debris.XOffSet
#alias Object.Value5		:	Debris.YOffSet

// Player Aliases
#alias Player.Value4		:	Player.InvincibleTimer

// States
#alias 0	:	R1_EGGMAN2_SETUP
#alias 1	:	R1_EGGMAN2_WAIT_PLAYER
#alias 2	:	R1_EGGMAN2_BODY_CONTROLLER
#alias 3	:	R1_EGGMAN2_DEFEATED
#alias 4	:	R1_EGGMAN2_BLANK
#alias 5	:	R1_EGGMAN2_RESTORE_BOUNDS
#alias 6	:	R1_EGGMAN2_SET_BOUNDARIES
#alias 7	:	R1_EGGMAN2_SPAWN_HVC

// EGG-HVC-001 Body States
#alias 1	:	BOSSBODY2_DESCEND
#alias 5	:	BOSSBODY2_MOVE_BACKWARDS
#alias 6	:	BOSSBODY2_PREPARE_CHARGING
#alias 7	:	BOSSBODY2_DESTROYED

// EGG-HVC-001 Arm Joint States
#alias 5	:	BOSSARMJOINT2_DESTROYED
// EGG-HVC-001 Arm States
#alias 2	:	BOSSARM2_DESTROYED
// EGG-HVC-001 Bumper States
#alias 2	:	BOSSBUMPER2_DESTROYED
// EGG-HVC-001 Leg Joint States
#alias 12	:	BOSSLEGJOINT2_DESTROYED
// EGG-HVC-001 Leg States
#alias 7	:	BOSSLEG2_DESTROYED
// EGG-HVC-001 Foot States
#alias 6	:	BOSSFOOT2_DESTROYED
// EGG-HVC-001 Shoulder States
#alias 2	:	BOSSSHOULDER2_DESTROYED

// Eggman Animations
#alias -1	:	EGGANI_NONE
#alias  0	:	EGGANI_IDLE
#alias  1	:	EGGANI_LAUGH
#alias  2	:	EGGANI_HURT
#alias  3	:	EGGANI_DESTROYED

// EGG-HVC-001 Body Bit 0 Aliases
#alias 0	:	KEY_EVENT_FALSE
#alias 1	:	KEY_EVENT_TRUE

// EGG-HVC-001 Body Bit 1 Aliases
#alias 0	:	MOVE_FORWARD
#alias 1	:	MOVE_BACKWARDS

// EGG-HVC-001 Body Bit 6 Aliases
#alias 0	:	LEFT_LEG_ACTIVE
#alias 1	:	RIGHT_LEG_ACTIVE

// Global SFX
#alias 22	:	SFX_G_EXPLOSION

// Priority
#alias 1	:	PRIORITY_ACTIVE

// Function declarations
#function R1_EGGMAN2_Oscillation

function R1_EGGMAN2_Oscillation
	switch Object.Oscillation
	case 0
		TempValue0 =  1
		break
	case 1
		TempValue0 =  0
		break
	case 2
		TempValue0 =  2
		break
	case 3
		TempValue0 =  0
		break
	case 4
		TempValue0 =  3
		break
	case 5
		TempValue0 =  0
		break
	case 6
		TempValue0 =  4
		break
	case 7
		TempValue0 =  5
		break
	case 8
		TempValue0 =  5
		break
	case 9
		TempValue0 =  5
		break
	case 10
		TempValue0 =  6
		break
	case 11
		TempValue0 =  10
		break
	case 12
		TempValue0 =  4
		break
	case 13
		TempValue0 =  14
		break
	case 14
		TempValue0 =  8
		break
	case 15
		TempValue0 =  50
		break
	case 16
		TempValue0 =  5
		break
	case 17
		TempValue0 =  14
		break
	case 18
	case 19
		TempValue0 = -1
		break
	end switch
end function


sub ObjectMain
	switch Object.State
	case R1_EGGMAN2_SETUP
		Object.Animation = EGGANI_NONE
		Object.Priority  = PRIORITY_ACTIVE

		Object.BodyEntity  = Object.EntityNo
		Object.BodyEntity += 1

		Object.ShoulderEntity  = Object.EntityNo
		Object.ShoulderEntity += 8

		Object.NewXBoundary1Value = BossBody.iXPos

		Object.NewXBoundary2Value  = Object.NewXBoundary1Value
		Object.NewXBoundary2Value += 320

		TempValue0   = Screen.CenterX
		TempValue0  -= 160
		TempValue0 >>= 1
		Object.NewXBoundary2Value += TempValue0

		Object.SavedXBoundary2 = Stage.XBoundary2
		Object.SavedYBoundary2 = Stage.YBoundary2

		Stage.NewXBoundary2 = Object.NewXBoundary2Value

		Object.State = R1_EGGMAN2_WAIT_PLAYER
		break

	case R1_EGGMAN2_WAIT_PLAYER
		TempValue0 = Object.NewXBoundary1Value
		if Screen.CenterX > 160
			TempValue0 += 180
		else
			TempValue0 += 160
		end if
		TempValue0 <<= 16

		if Player.XPos > TempValue0
			TempValue0  = Object.YPos
			TempValue0 += 0xC00000

			if Player.YPos > TempValue0
				Stage.NewXBoundary1 = Object.NewXBoundary1Value
				Stage.YBoundary2   = Player.YPos
				Stage.YBoundary2 >>= 16
				Stage.YBoundary2  += 160

				if Stage.YBoundary2 < 256
					Stage.YBoundary2 = 256
				end if

				Object.State = R1_EGGMAN2_SET_BOUNDARIES
			end if

		end if
		break

	case R1_EGGMAN2_BODY_CONTROLLER
		if Object.Timer > 0
			Object.Timer--
		end if
		if BossBody.State > 2
			if Object.FaceTimer > 0
				Object.FaceTimer--
			else
				Object.Animation = EGGANI_IDLE
			end if
		end if

		ArrayPos0 = Object.BodyEntity
		GetBit(TempValue0, Body[ArrayPos0].BitHolder, 0)
		if TempValue0 == KEY_EVENT_TRUE
			if Body[ArrayPos0].State == BOSSBODY2_PREPARE_CHARGING
				// Object +15 is a blank object used to hold values
				Object.YPos         = Object[+15].Value0
				BossBody.YPos       = Object[+15].Value1
				BossLegJointR.YPos  = Object[+15].Value2
				BossLegR.YVelocity  = Object[+15].Value3
				BossLegR.YPos       = Object[+15].YPos
				BossFootR.YVelocity = Object[+15].Value4
				BossLegJointL.YPos  = Object[+15].Value5
				BossLegL.YVelocity  = Object[+15].Value6
				BossFootL.YVelocity = Object[+15].Value7
				GetBit(TempValue0, Body[ArrayPos0].BitHolder, 6)
				if TempValue0 == LEFT_LEG_ACTIVE
					SetBit(Body[ArrayPos0].BitHolder, 6, RIGHT_LEG_ACTIVE)
				else
					SetBit(Body[ArrayPos0].BitHolder, 6, LEFT_LEG_ACTIVE)
				end if
			end if

			SetBit(Body[ArrayPos0].BitHolder, 0, KEY_EVENT_FALSE)
			if Object.Oscillation == 2
				SetBit(Object.BitHolder, 3, 1) // This may be a typo... it's supposed to be changing the [Boss Body]'s value instead I imagine?
			end if

			SetBit(Body[ArrayPos0].BitHolder, 1, MOVE_FORWARD)
			Object.Oscillation += 2
			CallFunction(R1_EGGMAN2_Oscillation)
			if TempValue0 < 0
				Object.Oscillation = 6
			end if

			CallFunction(R1_EGGMAN2_Oscillation)
			if TempValue0 == 6	// Go back
				// Object +15 is a blank object used to hold values
				Object[+15].Value0 = Object.YPos
				Object[+15].Value1 = BossBody.YPos
				Object[+15].Value2 = BossLegJointR.YPos
				Object[+15].Value3 = BossLegR.YVelocity
				Object[+15].YPos   = BossLegR.YPos
				Object[+15].Value4 = BossFootR.YVelocity
				Object[+15].Value5 = BossLegJointL.YPos
				Object[+15].Value6 = BossLegL.YVelocity
				Object[+15].Value7 = BossFootL.YVelocity
			end if

			if Body[ArrayPos0].State == BOSSBODY2_MOVE_BACKWARDS
				if TempValue0 == 4
					GetBit(TempValue1, Body[ArrayPos0].BitHolder, 6)
					if TempValue1 == LEFT_LEG_ACTIVE
						SetBit(Body[ArrayPos0].BitHolder, 6, RIGHT_LEG_ACTIVE)
					else
						SetBit(Body[ArrayPos0].BitHolder, 6, LEFT_LEG_ACTIVE)
					end if
				end if
			end if
			
			Body[ArrayPos0].State = TempValue0
			Object.Oscillation++
			CallFunction(R1_EGGMAN2_Oscillation)
			Object.Oscillation--
			Body[ArrayPos0].ChargeStart = TempValue0
		end if
		break

	case R1_EGGMAN2_DEFEATED
		if Object.Timer < 160
			Object.Timer++

			TempValue0  = Object.Timer
			TempValue0 &= 3
			if TempValue0 == 3
				Rand(TempValue0, 128)
				TempValue0  -= 64
				TempValue0 <<= 16
				TempValue0  += Object.XPos

				Rand(TempValue1, 64)
				TempValue1  -= 32
				TempValue1 <<= 16
				TempValue1  += Object.YPos

				CreateTempObject(TypeName[Explosion], 0, TempValue0, TempValue1)

				PlaySfx(SFX_G_EXPLOSION, false)
			end if

			if Object.Timer == 94
				TempValue0 = Object.EntityNo
				TempValue0++

				Player.Score += 1000
				ResetObjectEntity(TempValue0, TypeName[Eggman Jetpack], 0, Object.XPos, Object.YPos)
				Object.Animation = EGGANI_DESTROYED

#platform: Use_Haptics
				HapticEffect(76, 0, 0, 0)
#endplatform
			end if
		end if
		break

	case R1_EGGMAN2_BLANK
		break

	case R1_EGGMAN2_RESTORE_BOUNDS
		if Object.Timer < 120
			Music.Volume--

			Stage.YBoundary2 += 4
			Stage.XBoundary2 += 6

			Object.Timer++
		else
			Stage.YBoundary2 = Object.SavedYBoundary2
			Stage.XBoundary2 = Object.SavedXBoundary2
			PlayMusic(0)
			Object.Type = TypeName[Blank Object]
		end if
		break

	case R1_EGGMAN2_SET_BOUNDARIES
		if Stage.YBoundary2 > BossBody.iYPos
			Stage.YBoundary2 -= 2
			Stage.YBoundary1 += 2
		else
			Stage.YBoundary2 = BossBody.iYPos
			TempValue0 = BossBody.iYPos
			TempValue0 -= Screen.YSize
			if TempValue0 < 0
				TempValue0 = 0
			end if
			Stage.NewYBoundary1 = TempValue0

			Object.Timer = 0

			Object.State = R1_EGGMAN2_SPAWN_HVC
		end if
		break

	case R1_EGGMAN2_SPAWN_HVC
		if Object.Timer < 120
			if Object.Timer < 100
				Music.Volume--
			else
				if Object.Timer == 100
					PlayMusic(4)
				end if
			end if
			Object.Timer++
		else
			Object.Timer = 0
			Object.State = R1_EGGMAN2_BODY_CONTROLLER
			Object.Animation = EGGANI_LAUGH

			ArrayPos0 = Object.EntityNo
			ArrayPos0++

			if Object.BossSpeed < 1
				Object.BossSpeed = 10	// Sets the speed of the boss for the mission
			end if

			ResetObjectEntity(ArrayPos0, TypeName[BossBody2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority = PRIORITY_ACTIVE

			Body[ArrayPos0].FaceEntity = Object.EntityNo

			Body[ArrayPos0].LegJointREntity  = Object.EntityNo
			Body[ArrayPos0].LegJointREntity += 2

			Body[ArrayPos0].LegJointLEntity  = Object.EntityNo
			Body[ArrayPos0].LegJointLEntity += 5
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossLegJoint2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority = PRIORITY_ACTIVE

			// Boss Body
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 1

			// Leg R
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 3
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossLeg2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority = PRIORITY_ACTIVE

			// Leg Joint R
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 2

			// Foot R
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 4
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossFoot2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority = PRIORITY_ACTIVE

			// Leg R
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 3

			// Boss Body
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 1
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossLegJoint2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority  = PRIORITY_ACTIVE
			BossPart[ArrayPos0].DrawOrder = 2

			// Boss Body
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 1

			// Leg L
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 6
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossLeg2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority  = PRIORITY_ACTIVE
			BossPart[ArrayPos0].DrawOrder = 2

			// Leg Joint L
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 5

			// Foot L
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 7
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossFoot2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority  = PRIORITY_ACTIVE
			BossPart[ArrayPos0].DrawOrder = 2

			// Leg L
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 6

			// Boss Body
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 1
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossShoulder], 0, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority = PRIORITY_ACTIVE
			BossPart[ArrayPos0].DrawOrder = 4

			Shoulder[ArrayPos0].FaceEntity = Object.EntityNo

			// Arm Joint L
			Shoulder[ArrayPos0].ArmJointLEntity  = Object.EntityNo
			Shoulder[ArrayPos0].ArmJointLEntity += 9

			// Arm Joint R
			Shoulder[ArrayPos0].ArmJointREntity  = Object.EntityNo
			Shoulder[ArrayPos0].ArmJointREntity += 12
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossArmJoint2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority = PRIORITY_ACTIVE

			// Shoulder
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 8

			// Arm L
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 10
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossArm2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority = PRIORITY_ACTIVE

			// Arm Joint L
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 9

			// Bumper L
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 11
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossBumper2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority = PRIORITY_ACTIVE

			// Arm L
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 10

			// Boss Body
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 1
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossArmJoint2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority  = PRIORITY_ACTIVE
			BossPart[ArrayPos0].DrawOrder = 2

			// Shoulder
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 8

			// Arm R
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 13
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossArm2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority  = PRIORITY_ACTIVE
			BossPart[ArrayPos0].DrawOrder = 2

			// Arm Joint R
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 12

			// Bumper R
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 14
			ArrayPos0++

			ResetObjectEntity(ArrayPos0, TypeName[BossBumper2], Object.BossSpeed, Object.XPos, Object.YPos)
			BossPart[ArrayPos0].Priority  = PRIORITY_ACTIVE
			BossPart[ArrayPos0].DrawOrder = 2

			// Arm R
			BossPart[ArrayPos0].PrevEntity  = Object.EntityNo
			BossPart[ArrayPos0].PrevEntity += 13

			// Boss Body
			BossPart[ArrayPos0].NextEntity  = Object.EntityNo
			BossPart[ArrayPos0].NextEntity += 1
			ArrayPos0++
		end if
		break
	end switch
end sub


sub ObjectPlayerInteraction
	TempValue0   = Player.CollisionRight
	TempValue0 <<= 16
	TempValue0  += Player.XPos
	
	TempValue1   = Stage.XBoundary2
	TempValue1 <<= 16
	if TempValue0 > TempValue1
		Player.XVelocity = 0
		Player.Speed = 0
		Player.XPos  = TempValue1

		TempValue0   = Player.CollisionRight
		TempValue0 <<= 16
		Player.XPos -= TempValue0
	end if

	if Object.State > R1_EGGMAN2_WAIT_PLAYER
		if Player.Animation != ANI_HURT
			if Object.Hit < 3
				PlayerObjectCollision(C_ENEMY, -28, -28, 28, 28)
				if CheckResult == true
					TempValue2  = false
					CheckEqual(Player.Animation, ANI_JUMPING)
					TempValue2 |= CheckResult
					CheckEqual(Player.Animation, ANI_HAMMER_JUMP)
					TempValue2 |= CheckResult
					CheckEqual(Player.Animation, ANI_HAMMER_DASH)
					TempValue2 |= CheckResult
					if TempValue2 == true
						Player.XVelocity   = Player.XPos
						Player.XVelocity  -= Object.XPos
						Player.XVelocity >>= 3
						Player.Speed = Player.XVelocity

						Player.YVelocity   = Player.YPos
						Player.YVelocity  -= Object.YPos
						Player.YVelocity >>= 3

						if Object.Timer == 0
							if BossBody.State != BOSSBODY2_DESCEND
								PlaySfx(SFX_G_EXPLOSION, false)
								
								switch Object.Hit
								case 0
									Object.Hit++
									Object.Timer = 30
									Object.Animation = EGGANI_HURT
									Object.FaceTimer = 120

									ArrayPos0 = Object.ShoulderEntity
									ArrayPos1 = Shoulder[ArrayPos0].ArmJointREntity
									Debris[ArrayPos1].State   = BOSSARMJOINT2_DESTROYED
									Debris[ArrayPos1].XOffSet = 0x10000
									Debris[ArrayPos1].YOffSet = 0x20000

									// Arm Joint R to Arm R
									ArrayPos0 = BossPart[ArrayPos1].NextEntity
									Debris[ArrayPos0].State   = BOSSARM2_DESTROYED
									Debris[ArrayPos0].XOffSet = 0
									Debris[ArrayPos0].YOffSet = 0x10000

									// Arm R to Bumper R
									ArrayPos1 = BossPart[ArrayPos0].NextEntity
									Debris[ArrayPos1].State   = BOSSBUMPER2_DESTROYED
									Debris[ArrayPos1].XOffSet = 0
									Debris[ArrayPos1].YOffSet = -0x18000
									break

								case 1
									Object.Hit++
									Object.Timer = 30
									Object.Animation = EGGANI_HURT
									Object.FaceTimer = 120

									ArrayPos0 = Object.ShoulderEntity
									ArrayPos1 = Shoulder[ArrayPos0].ArmJointLEntity
									Debris[ArrayPos1].State   = BOSSARMJOINT2_DESTROYED
									Debris[ArrayPos1].XOffSet = 0x10000
									Debris[ArrayPos1].YOffSet = 0x20000
									
									// Arm Joint L to Arm L
									ArrayPos0 = BossPart[ArrayPos1].NextEntity
									Debris[ArrayPos0].State   = BOSSARM2_DESTROYED
									Debris[ArrayPos0].XOffSet = 0
									Debris[ArrayPos0].YOffSet = 0x10000

									// ArmL to Bumper L
									ArrayPos1 = BossPart[ArrayPos0].NextEntity
									Debris[ArrayPos1].State   = BOSSBUMPER2_DESTROYED
									Debris[ArrayPos1].XOffSet =  0
									Debris[ArrayPos1].YOffSet = -0x18000
									break

								case 2
									Object.Hit++
									Object.State = R1_EGGMAN2_DEFEATED
									Object.Animation = EGGANI_HURT

									Stage.TimeEnabled = false

									ArrayPos0 = Object.BodyEntity
									Body[ArrayPos0].State = BOSSBODY2_DESTROYED

									ArrayPos0 = Object.ShoulderEntity
									Debris[ArrayPos0].State = BOSSSHOULDER2_DESTROYED
									Debris[ArrayPos0].XOffSet = -0x10000
									Debris[ArrayPos0].YOffSet = -0x20000

									ArrayPos0 = Object.BodyEntity
									ArrayPos1 = Body[ArrayPos0].LegJointREntity
									Debris[ArrayPos1].State = BOSSLEGJOINT2_DESTROYED
									Debris[ArrayPos1].XOffSet =  0
									Debris[ArrayPos1].YOffSet = -0x28000

									// Leg Joint R to Leg R
									ArrayPos0 = BossPart[ArrayPos1].NextEntity
									Debris[ArrayPos0].State = BOSSLEG2_DESTROYED
									Debris[ArrayPos0].XOffSet =  0
									Debris[ArrayPos0].YOffSet = -0x20000

									// Leg R to Foot R
									ArrayPos1 = BossPart[ArrayPos0].NextEntity
									Debris[ArrayPos1].State = BOSSFOOT2_DESTROYED
									Debris[ArrayPos1].XOffSet =  0
									Debris[ArrayPos1].YOffSet = -0x23000

									ArrayPos0 = Object.BodyEntity
									ArrayPos1 = Body[ArrayPos0].LegJointLEntity
									Debris[ArrayPos1].State = BOSSLEGJOINT2_DESTROYED
									Debris[ArrayPos1].XOffSet =  0
									Debris[ArrayPos1].YOffSet = -0x28000

									// Leg Joint L to Leg L
									ArrayPos0 = BossPart[ArrayPos1].NextEntity
									Debris[ArrayPos0].State = BOSSLEG2_DESTROYED
									Debris[ArrayPos0].XOffSet =  0
									Debris[ArrayPos0].YOffSet = -0x20000

									// Leg L to Foot L
									ArrayPos1 = BossPart[ArrayPos0].NextEntity
									Debris[ArrayPos1].State = BOSSFOOT2_DESTROYED
									Debris[ArrayPos1].XOffSet =  0
									Debris[ArrayPos1].YOffSet = -0x23000

									break
								end switch
							end if
						end if
					else
						if Player.InvincibleTimer == 0
							Object.Animation = EGGANI_LAUGH
							Object.FaceTimer = 180
							Player.State = Player_State_GotHit
							if Player.XPos > Object.XPos
								Player.Speed =  0x20000
							else
								Player.Speed = -0x20000
							end if
						end if
					end if
				end if
			end if
		end if
	end if
end sub


sub ObjectDraw
	switch Object.Animation
	case EGGANI_IDLE
		DrawSprite(0)
		break

	case EGGANI_LAUGH
		if Object.FrameTimer < 8
			Object.FrameTimer = 8
		else
			if Object.FrameTimer > 23
				Object.FrameTimer = 8
			end if
		end if

		Object.Frame = Object.FrameTimer
		Object.Frame >>= 3

		Object.FrameTimer++

		DrawSprite(Object.Frame)
		break

	case EGGANI_HURT
		if Object.FrameTimer < 12
			Object.FrameTimer = 12
		else
			if Object.FrameTimer > 27
				Object.FrameTimer = 12
			end if
		end if

		Object.Frame = Object.FrameTimer
		Object.Frame >>= 2

		Object.FrameTimer++

		DrawSprite(Object.Frame)
		break
	end switch
end sub


sub ObjectStartup
	LoadSpriteSheet("R1/Objects3.gif")

	SpriteFrame(-28, -24, 40, 16, 206, 1)		// #0 - Eggman Idle

	SpriteFrame(-28, -24, 40, 16, 206, 18)		// #1 - Eggman Laugh Frame 0
	SpriteFrame(-28, -24, 40, 16, 206, 35)		// #2 - Eggman Laugh Frame 1

	SpriteFrame(-28, -40, 40, 32, 124, 1)		// #3 - Eggman Hurt Frame 0
	SpriteFrame(-28, -24, 40, 16, 206, 52)		// #4 - Eggman Hurt Frame 1
	SpriteFrame(-28, -40, 40, 32, 165, 1)		// #5 - Eggman Hurt Frame 2
	SpriteFrame(-28, -24, 40, 16, 206, 69)		// #6 - Eggman Hurt Frame 3

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Boss Face]
			Object[ArrayPos0].Type = TypeName[BossFace2]
			ArrayPos0 = 2000
		end if
		ArrayPos0++
	loop
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R1/Objects3.gif")
	SpriteFrame(-28, -24, 40, 16, 206, 1)		// #0 - Eggman Idle

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
