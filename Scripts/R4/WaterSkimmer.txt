//----------------Sonic CD Water Skimmer Script---------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0		:	Object.Timer
#alias Object.Value1		:	Object.XVelocity
#alias Object.Value2		:	Object.BulletTimer

#alias Object.PropertyValue	:	Object.Quality

// States
#alias 0	:	WATERSKIMMER_DASH_LEFT
#alias 1	:	WATERSKIMMER_FLOAT_LEFT
#alias 2	:	WATERSKIMMER_TURN_TO_RIGHT
#alias 3	:	WATERSKIMMER_CHARGE_RIGHT
#alias 4	:	WATERSKIMMER_DASH_RIGHT
#alias 5	:	WATERSKIMMER_FLOAT_RIGHT
#alias 6	:	WATERSKIMMER_TURN_TO_LEFT
#alias 7	:	WATERSKIMMER_CHARGE_LEFT

// Stage SFX
#alias 1	:	SFX_S_SHOT

// Badnik Quality / Property Values
#alias 0	:	GOOD_QUALITY
#alias 1	:	BAD_QUALITY


sub ObjectMain
	if Object.Quality == GOOD_QUALITY
		switch Object.State
		case WATERSKIMMER_DASH_LEFT
			if Object.XVelocity > -0x18000
				Object.XVelocity -= 0x400
				Object.XPos += Object.XVelocity
			else
				Object.State++
			end if
			Object.Frame++
			Object.Frame %= 22
			break

		case WATERSKIMMER_FLOAT_LEFT
			if Object.XVelocity < 0
				Object.XVelocity += 0x118
				Object.XPos += Object.XVelocity
			else
				Object.XVelocity = 0
				Object.State++
				Object.Timer = 60
			end if
			break

		case WATERSKIMMER_TURN_TO_RIGHT
			if Object.Timer > 0
				Object.Timer--
			else
				Object.State++
				Object.Direction = FACING_LEFT
				Object.Timer = 60
			end if
			break

		case WATERSKIMMER_WAIT
			if Object.Timer > 0
				Object.Timer--
			else
				Object.State++
			end if
			break

		case WATERSKIMMER_DASH_RIGHT
			if Object.XVelocity < 0x18000
				Object.XVelocity += 0x400
				Object.XPos += Object.XVelocity
			else
				Object.State++
			end if
			Object.Frame++
			Object.Frame %= 22
			break

		case WATERSKIMMER_FLOAT_RIGHT
			if Object.XVelocity > 0
				Object.XVelocity -= 0x118
				Object.XPos += Object.XVelocity
			else
				Object.XVelocity = 0
				Object.State++
				Object.Timer = 60
			end if
			break

		case WATERSKIMMER_TURN_TO_LEFT
			if Object.Timer > 0
				Object.Timer--
			else
				Object.State++
				Object.Direction = FACING_RIGHT
				Object.Timer = 60
			end if
			break

		case WATERSKIMMER_CHARGE_LEFT
			if Object.Timer > 0
				Object.Timer--
			else
				Object.State = WATERSKIMMER_DASH_LEFT
			end if
			break

		end switch

		if Object.BulletTimer > 0
			Object.BulletTimer--
			if Object.BulletTimer == 0
				PlayStageSfx(SFX_S_SHOT, false)
				CreateTempObject(TypeName[SkimmerBullet], 0, Object.XPos, Object.YPos)
				if Object.Direction == FACING_RIGHT
					Object[TempObjectPos].XVelocity = -0x20000
				else
					Object[TempObjectPos].XVelocity =  0x20000
				end if
			end if
		end if
	else
		switch Object.State
		case WATERSKIMMER_DASH_LEFT
			if Object.XVelocity > -0x10000
				Object.XVelocity -= 0x400
				Object.XPos += Object.XVelocity
			else
				Object.State++
			end if
			Object.Frame++
			Object.Frame %= 22
			break

		case WATERSKIMMER_FLOAT_LEFT
			if Object.XVelocity < 0
				Object.XVelocity += 0x118
				Object.XPos += Object.XVelocity
			else
				Object.XVelocity = 0
				Object.State++
				Object.Timer = 60
			end if
			break

		case WATERSKIMMER_TURN_TO_RIGHT
			if Object.Timer > 0
				Object.Timer--
			else
				Object.State++
				Object.Direction = FACING_LEFT
				Object.Timer = 60
			end if
			break

		case WATERSKIMMER_CHARGE_RIGHT
			if Object.Timer > 0
				Object.Timer--
			else
				Object.State++
			end if
			break

		case WATERSKIMMER_DASH_RIGHT
			if Object.XVelocity < 0x10000
				Object.XVelocity += 0x400
				Object.XPos += Object.XVelocity
			else
				Object.State++
			end if
			Object.Frame++
			Object.Frame %= 22
			break

		case WATERSKIMMER_FLOAT_RIGHT
			if Object.XVelocity > 0
				Object.XVelocity -= 0x118
				Object.XPos += Object.XVelocity
			else
				Object.XVelocity = 0
				Object.State++
				Object.Timer = 60
			end if
			break

		case WATERSKIMMER_TURN_TO_LEFT
			if Object.Timer > 0
				Object.Timer--
			else
				Object.State++
				Object.Direction = FACING_RIGHT
				Object.Timer = 60
			end if
			break

		case WATERSKIMMER_CHARGE_LEFT
			if Object.Timer > 0
				Object.Timer--
			else
				Object.State = WATERSKIMMER_DASH_LEFT
			end if
			break
		end switch
	end if

	Object.YPos   = Stage.WaterLevel
	Object.YPos  -= 10
	Object.YPos <<= 16

	CallFunction(StageSetup_CheckGoodFuture)	// Check if this should be a flower
end sub


sub ObjectPlayerInteraction
	PlayerObjectCollision(C_TOUCH, -12, -8, 12, 8)
	if CheckResult == true
		CallFunction(Player_BadnikBreak)
	end if

	if Object.BulletTimer == 0
		if Object.Direction == FACING_RIGHT
			PlayerObjectCollision(C_TOUCH, -96, -16, 0, 16)
			if CheckResult == true
				Object.BulletTimer = 120
			end if
		else
			PlayerObjectCollision(C_TOUCH, 0, -16, 96, 16)
			if CheckResult == true
				Object.BulletTimer = 120
			end if
		end if
	end if
end sub


sub ObjectDraw
	if Object.Quality == GOOD_QUALITY
		TempValue0  = Object.Frame
		TempValue0 /= 11
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
	else
		TempValue0  = Object.Frame
		TempValue0 /= 11
		TempValue0 += 2
		DrawSpriteFX(TempValue0, FX_FLIP, Object.XPos, Object.YPos)
	end if
end sub


sub ObjectStartup
	LoadSpriteSheet("R4/Objects.gif")

	SpriteFrame(-20, -12, 32, 24, 67, 231)	// #0 - Water Skimmer frame 0
	SpriteFrame(-20, -12, 32, 24, 100, 231)	// #1 - Water Skimmer frame 1
	
	SpriteFrame(-20, -12, 32, 24, 1, 231)	// #2 - Broken Water Skimmer frame 0
	SpriteFrame(-20, -12, 32, 24, 34, 231)	// #3 - Broken Water Skimmer frame 1
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
