//---------------Sonic CD Boss Bubble 2 Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
#alias Object.Value0	:	Object.TImer

// States
#alias 0	:	BOSSBUBBLE2_GROWTH_1
#alias 1	:	BOSSBUBBLE2_GROWTH_2
#alias 2	:	BOSSBUBBLE2_BREATHABLE
#alias 3	:	BOSSBUBBLE2_CONSUMED
#alias 4	:	BOSSBUBBLE2_DISSAPEAR

// Stage SFX
#alias 6	:	SFX_S_BREATHING


sub ObjectMain
	switch Object.State
	case BOSSBUBBLE2_GROWTH_1
		if Object.TImer < 20
			Object.TImer++
		else
			Object.State++
		end if
		break

	case BOSSBUBBLE2_GROWTH_2
		Object.State++
		break

	case BOSSBUBBLE2_BREATHABLE	// only here for display purposes, waits for player to interact with it
		break

	case BOSSBUBBLE2_CONSUMED
		if Object.TImer < 22
			Object.TImer++
		else
			Object.State++
		end if
		break

	case BOSSBUBBLE2_DISSAPEAR
		if Object.TImer < 42
			Object.TImer++
		else
			Object.Type = TypeName[Blank Object]
		end if
		break

	end switch
end sub


sub ObjectPlayerInteraction
	if Object.State == BOSSBUBBLE2_BREATHABLE
		if Player.Animation == ANI_JUMPING
			PlayerObjectCollision(C_TOUCH, -8, -8, 8, 8)
			if CheckResult == true
				Object.State = BOSSBUBBLE2_CONSUMED
				FlipSign(Player.Speed)
				FlipSign(Player.XVelocity)

				Player.YVelocity = 0x40000

				Player.Animation = ANI_BREATHING
				Player.AirTimer  = 0
				
				PlayStageSfx(SFX_S_BREATHING, false)
				
#platform: Use_Haptics
				HapticEffect(19, 0, 0, 0)
#endplatform

			end if
		end if

		if Player.Animation == ANI_FLYING
			PlayerObjectCollision(C_TOUCH, -8, -8, 8, 8)
			if CheckResult == true
				Object.State = BOSSBUBBLE2_CONSUMED
				FlipSign(Player.Speed)
				FlipSign(Player.XVelocity)

				Player.YVelocity = 0x40000
				
				Player.Animation = ANI_BREATHING
				
#platform: Use_Origins
				Player.State = Player_State_Air_NoDropDash
#endplatform

#platform: Use_Standalone
				Player.State = Player_State_Air
#endplatform

				Player.AirTimer = 0
				PlayStageSfx(SFX_S_BREATHING, false)
				
#platform: Use_Haptics
				HapticEffect(19, 0, 0, 0)
#endplatform
			end if
		end if
	end if
end sub


sub ObjectDraw
	DrawSprite(Object.State)
end sub


sub ObjectStartup
	LoadSpriteSheet("R4/Objects2.gif")

	SpriteFrame(-8, -8, 16, 16, 99, 118)		// #0 - Small bubble
	SpriteFrame(-12, -12, 24, 24, 116, 126)		// #1 - Medium bubble
	SpriteFrame(-16, -16, 32, 32, 222, 115)		// #2 - Big bubble
	SpriteFrame(-16, -16, 32, 32, 189, 115)		// #3 - Big bubble inhaled
	SpriteFrame(-16, -16, 32, 32, 156, 126)		// #4 - Big bubler bursted
end sub


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("R4/Objects2.gif")
	SpriteFrame(-16, -16, 32, 32, 222, 115)		// #2 - Big bubble

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
